//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                     brAthena Script                    |
// |--------------------------------------------------------|
// | Nome do Script: Sistema de Batalha - KvM 1-59          |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.2/1.0                                |
// |--------------------------------------------------------|
// | Descrição: NPCs do KvM 1-59.                           |
// | - NPC's da Sala de Espera                              |
// | - Mecanismo de Início                                  |
// | - Engine do Campo de Batalha do KvM                    |
// | - Recompensas do Campo de Batalha                      |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// |  1.0 Script Traduzido [Tyler]                          |
// |  - Anotações                                           |
// \________________________________________________________/

// NPC's da Sala de Espera
//============================================================
bat_room,197,226,5	script	Sala de Espera do KVM#a2::KvM02R_Guillaume	418,{
	end;

OnInit:
	waitingroom "Estação de Batalha de 5 jogadores",5,"KvM02_BG::OnGuillaumeJoin",1;
	end;

OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("bat_c02",52,129,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bat_room,197,220,0	warp	#kvm601	2,2,bat_room,154,150

bat_room,197,205,3	script	Sala de Espera do KVM#b2::KvM02R_Croix	414,{
	end;

OnInit:
	waitingroom "Estação de Batalha de 5 jogadores",5,"KvM02_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("bat_c02",147,55,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

bat_room,197,211,0	warp	#kvm602	2,2,bat_room,154,150

// Mecanismo de Início
//============================================================
-	script	KVM02::CellEffect2	-1,{
	end;

OnKvM02One:
	misceffect EF_CHANGECOLD;
	end;

OnKvM02Two:
	misceffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	139
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	139
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	139
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	139
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	139
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	139
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	139

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	139
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	139
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	139
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	139
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	139
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	139
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	139

bat_c02,54,128,3	script	#A_camp_start02	139,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start02";
	end;

OnEnable:
	enablenpc "#A_camp_start02";
	end;

OnDisable:
	disablenpc "#A_camp_start03";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c02,146,56,3	script	#B_camp_start02	139,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start02";
	end;

OnEnable:
	enablenpc "#B_camp_start02";
	end;

OnDisable:
	disablenpc "#B_camp_start02";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Mecanismo de Início
//============================================================
-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	disablenpc "Oficial do KVM#KVM02A";
	disablenpc "Oficial do KVM#KVM02B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM02_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
		else {
			mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM02BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;

OnStart:
	disablenpc "Oficial do KVM#KVM02A";
	disablenpc "Oficial do KVM#KVM02B";
	set $@KvM02BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Em 1 minuto, Batalha pelo KVM será iniciada.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O tempo limite para a Batalha pelo KvM é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Por favor prepare-se para a Batalha pelo KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Você já pode abençoar seus aliados.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start02::OnEnable";
	donpcevent "#B_camp_start02::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start02::OnDisable";
	donpcevent "#B_camp_start02::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c02", "30 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c02", "15 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c02", "10 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c02", "5 segundos restantes para a Batalha pelo KvM se iniciar.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c02", "Que começe a Batalha pelo KvM!",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "bat_c02","Não há jogadores suficientes para dar início a Batalha.",1,0x808080;
		stopnpctimer;
		donpcevent "KvM02_BG::OnStop";
		end;
	}
	set $@KvM02BG, 2; // Playing
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c02", "1 minuto restante para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c02", "30 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c02", "15 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c02", "10 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c02", "5 segundos restantes para a Batalha pelo KvM terminar.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c02", "A Batalha pelo KvM terminou!",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "O número de Croixes é "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "A batalha terminou em um empate.",bc_map,"0x00ff00";
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	mapannounce "bat_c02", "O General Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	mapannounce "bat_c02", "O Princípe Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	disablenpc "Oficial do KVM#KVM02A";
	disablenpc "Oficial do KVM#KVM02B";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "Oficial do KVM#KVM02A";
	disablenpc "Oficial do KVM#KVM02B";
	mapwarp "bat_c02","bat_room",154,150;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",5,5;
	set $@KvM02BG, 0;
	donpcevent "KvM02_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Por favor fale com o Oficial para adquirir seus Pontos do KvM.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O Oficial estará distribuindo os pontos durante 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Em 30 segundos, o Oficial será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "Se você não entrar em contato com o Oficial, você não receberá seus pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Por favor tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer:55000:
	mapannounce "bat_c02", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM02_BG::OnReset";
	end;
}

// Recompensas do Campo de Batalha
//============================================================
bat_c02,51,130,5	script	Oficial do KVM#KVM02A	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[Oficial do KvM]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 1";
			close2;
		}
		else
		{ // Lose
			mes "[Oficial do KvM]";
			mes "Eu sinto muito.";
			mes "Eu te desejo mais sorte na próxima vez.";
			mes "Você adquiriu os pontos dos perdedores: 0";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c02,148,53,1	script	Oficial do KVM#KVM02B	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[Oficial do KvM]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 1";
			close2;
		}
		else
		{ // Lose
			mes "[Oficial do KvM]";
			mes "Eu sinto muito.";
			mes "Eu te desejo mais sorte na próxima vez.";
			mes "Você adquiriu os pontos dos perdedores: 0";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}