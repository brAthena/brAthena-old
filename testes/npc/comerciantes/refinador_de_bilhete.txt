//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: refinador_de_bilhete.txt               |
//   |--------------------------------------------------------|
//   | Criado por: rA/brA Dev. Team                           |
//   |--------------------------------------------------------|
//   | Versões rA/brA: 1.0/1.1                                |
//   |--------------------------------------------------------|
//   | Descrição: Refinador que usa bilhete para refinar      |
//   | equipamentos em +5~9/+11 sem chance de falha.          |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [lututuiBR]                         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

prontera,184,177,6	script	Mestre do Refino	851,{
	disable_items;
	if (countitem(6238) || countitem(6228) || countitem(6229) || countitem(6230) || countitem(6231) || countitem(6456))
		set .@bWeaponUp,1;
	if (countitem(6239) || countitem(6232) || countitem(6233) || countitem(6234) || countitem(6235) || countitem(6457))
		set .@bArmorUp,1;
	if (!.@bWeaponUp && !.@bArmorUp) {
		mes "[Mestre do Refino]";
		mes "Olá!";
		mes "E aí, novidades?";
		mes "Sou um especialista em refinar itens, mas eu não trabalho mais.";
		next;
		switch(select("Seguir seu caminho.:Hmm... isso me deixa curioso.")) {
		case 1:
			mes "[Mestre do Refino]";
			mes "Cuide-se, aventureiro.";
			close;
		case 2:
			mes "[Mestre do Refino]";
			mes "Na verdade, eu às vezes ofereço serviços de refinamento a aventureiros com um ^006400Pergaminho de Refino^000000...";
			mes "Bye bye~!";
			close;
		}
	}
	emotion e_gasp;
	mes "[Mestre do Refino]";
	mes "Saudações!";
	mes "Eu posso refinar um item até o ^006400mesmo nível do seu ticket^000000.";
	mes "Não precisa se preocupar! Não há chance de quebrar seu item.";
	next;
	if(select("Voltarei depois.:Refinar item com ticket.") == 1) {
		mes "[Mestre do Refino]";
		mes "Okay.";
		mes "Você pode voltar depois.";
		close;
	}
	mes "[Mestre do Refino]";
	mes "Que equipamento gostaria de refinar?";
	next;
	setarray .@position$[1],"Cabeça topo","Vestimenta","Mão esquerda","Mão direita","Capa","Sapatos","Acessório 1","AcesSório 2","Cabeça meio","Cabeça baixo";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@i))?getequipname(.@i):.@position$[.@i]+"- [Vazio]")+":";
	set .@part, select(.@menu$);
	if (!getequipisequiped(.@part)) {
		mes "[Mestre do Refino]";
		mes "Você deve equipar o item que quer refinar.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion e_otl;
		mes "[Mestre do Refino]";
		mes "Me desculpe.";
		mes "É impossível refinar esse item.";
		close;
	}
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		setarray .@tickets[0],6457,6235,6234,6233,6232,6239;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Vestimenta";
		set .@check,.@bArmorUp;
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		setarray .@tickets[0],6456,6231,6230,6229,6228,6238;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Arma";
		set .@check,.@bWeaponUp;
		break;
	}
	if (!.@check) {
		emotion e_dots;
		mes "[Mestre do Refino]";
		mes "Se você quiser refinar este ^006400"+.@type$+"^000000, por favor venha com um ^006400"+.@type$+" Pergaminho de Refino^000000.";
		mes "See you later!";
		close;
	}
	mes "[Mestre do Refino]";
	mes "Por favor escolha qual ^006400"+.@type$+" Pergaminho de Refino^000000 você quer usar.";
	next;
	set .@menu$,"";
	for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1)
		set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
	set .@select, select(.@menu$)-1;
	set .@ticket_lv, .@levels[.@select];
	set .@ticket_id, .@tickets[.@select];
	if (countitem(.@ticket_id) == 0) {
		emotion e_what;
		mes "[Mestre do Refino]";
		mes getitemname(.@ticket_id)+" não está no seu inventário. Você o colocou no armazém?";
		mes "Por favor cheque novamente.";
		mes "Vejo você depois!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion e_swt2;
		mes "[Mestre do Refino]";
		mes "^8B4513Este item já está refinado ao máximo possível.^000000";
		mes "Por favor volte com um item menos refinado que seu Pergaminho.";
		close;
	}
	mes "[Mestre do Refino]";
	mes "Eu vou refinar ^006400"+getequipname(.@part)+"^8B4513 até o nível +"+.@ticket_lv+"^000000 com o ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "Devo continuar?";
	next;
	if(select("Não.:Sim.") == 1) {
		emotion e_dots;
		mes "[Mestre do Refino]";
		mes "Oh, você mudou de ideia.";
		mes "Ok.";
		mes "Você pode voltar depois.";
		close;
	}
	mes "[Mestre do Refino]";
	mes "Ótimo.";
	mes "Como desejar!";
	mes "Eu tenho um jeito especial para refinar...";
	mes ".......ka boom!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id))
		delitem .@ticket_id,1;
	else {
		next;
		mes "Erro!";
		mes "Por favor reporte isto.";
		close;
	}
	for(set .@i,getequiprefinerycnt(.@part); .@i<.@ticket_lv; set .@i,.@i+1)
		successrefitem .@part;
	next;
	emotion e_ho;
	mes "[Mestre do Refino]";
	mes "Certo, aqui está~";
	mes "Bem, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "Parabéns pelo seu extraordinário "+.@type$+".";
	mes "Você fica ÓTIMO!";
	mes "Adeus~!";
	close;
}
