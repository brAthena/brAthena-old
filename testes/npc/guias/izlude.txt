//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: izlude.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.2/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Izlude.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Protimus, lututuiBR & Holy]        |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

izlude,129,175,4	script#re	Guia#01izlude	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a ^8B4513Izlude^000000, a cidade-satélite de Prontera.";
	mes "Você precisa de ajuda para navegar pela cidade?";
	F_Navi("[Guia de Izlude]");
	next;
	set .@str$, "Gostaria de verificar quaisquer outros locais?";
	while (1) {
		switch(select("[ Instalações Principais ]:[ Mercadores & Ajudantes ]:Remover indicações do Mini-Mapa:Cancelar")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Academia Kelitula ]:[ Marina ]:[ Aeroplano Internacional ]:[ Arena ]:[ Loja de Equipamentos ]:[ Guilda dos Espadachins ]:[ Associação dos Mercenários ]:[ Forja ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "Aprendizes devem ir na "+F_Navi("Academia Kelitula","iz_ac01,100,39")+" para receber mais informações e treinamento.";
					mes .@str$;
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "Você pode usar a "+F_Navi("Marina","izlude_a,197,205","^B9062F")+" a nordeste para ir para ^8B4513Alberta^000000 ou para a ^8B4513Ilha Byalan^000000.";
					mes "Tenha cuidado com os ^8B4513Calabouços de Izlude^000000 na Ilha Byalan desde que ela é cheia de monstros terríveis.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,197,205,1,0xFFFF0000;
					next;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Aeroplano Internacional","izlude,202,75","^0000FF")+" ao sudeste vai para ^8B4513Juno^000000 e ^8B4513Rachel^000000.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,210,73,2,0xFF0A82FF;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "A "+F_Navi("Arena","izlude,213,161","^FF5400")+" no norte é um local perfeito para tesar suas habilidades de combate.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,213,161,3,0xFF5400;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Loja de Equipamentos","izlude,110,182","^006400");
					viewpoint 1,108,179,4,0xFFAAFF00;
					next;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A "+F_Navi("Guilda dos Espadachins","izlude,51,172")+" é onde você pode ^800080mudar de classe para Espadachim.^000000";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Assossiação dos Mercenários","izlude,97,125","^B9062F"); 
					viewpoint 1,97,125,6,0xFF0000;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("Forja","izlude,153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Oficial de Teleporte do Éden ]:[ Funcionária Kafra ]:[ Hipnotizadora ]:[ Ajudante de Recém-Casados ]:[ Guilda dos Assistentes ]:[ Loja de Bichinhos ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Oficial de Teleporte do Éden","izlude,131,148","^0000FF")+" está atualmente dentro da Guilda dos Espadachins.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,131,148,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Funcionária Kafra","izlude,128,148","^FF5400"),1;
					viewpoint 1,128,148,9,0xFF5400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Hipnotizadora","izlude,127,175"),1;
					viewpoint 1,127,175,10,0x006400;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Se você gostaria de estar em Lua de Mel em ^8B4513Jawaii^000000, o paraíso dos recém-casados, por favor, fale com a "+F_Navi("Ajudante de Recém Casados","izlude,180,224","^B9062F")+".";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,180,224,11,0xFF0000;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Guilda dos Assistentes","izlude,47,170","^0000FF");
					viewpoint 1,47,170,12,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Loja de Bixinhos","izlude_in,72,98"),1;
					viewpoint 1,108,179,13,0x006400;
					next;
					break; 
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Certo, removerei todas as indicações do seu mini-mapa.";
			mes "Há mais alguma coisa que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Aproveite sua estadia Adeus~!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;

L_Mark:
	mes "[Izlude Guide]";
	mes "Deixe-me marcar a localização de "+getarg(0)+" no seu mini-mapa.";
	mes "Gostaria de checar quaisquer outras localizações?";
	return; 
}

izlude,121,87,6	script#vpo	Guia#iz	105,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a Izlude, a cidade-satélite de Prontera.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade.";
	mes "Se você tem alguma dúvida, consulte-nos.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Indicações.:Limpar indicações do minimapa.:Tutorial.:Cancelar")) {
		case 1:
			mes "[Guia de Izlude]";
			mes "Por favor, selecione uma das opções.";
			if (.@compass_check == 0) {
				mes "Você gostaria que as indicações permaneçam no mini-mapa?";
				next;
				if (select("Sim.:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Associação dos Espadachins^000000.:Sede dos Espadachins.:Arena.:Marina de Izlude.:Loja de Armas.:Loja de Ferramentas.:Cancelar.")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "A Associação se encontra na ilha do extremo oeste.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "A Sede se encontra na ilha do extremo leste.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "A Arena se encontra no extremo norte de Izlude.";
					mes "Porém ela ainda não está funcionando.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Da Marina <NAVI>[Marina]<INFO>izlude_a,200,180</INFO></NAVI> é possível ir até Alberta ou a ilha de Byalan, nosso calabouço.";
					mes "Ela se localiza ao nordeste.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Guia de Izlude]";
					mes "Você pode facilmente encontrar a <NAVI>[Loja de Armas]<INFO>izlude,111,149</INFO></NAVI> a noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A <NAVI>[Loja de Utilidades]<INFO>izlude,148,148</INFO></NAVI> não deve ser difícil de encontrar na parte nordeste da cidade.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Guia de Izlude]";
					mes "Se você deseja apagar as indicações do seu mini-mapa, por favor selecione 'Limpar indicações do minimapa'.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Nós fomos exaustivamente treinados para serví-lo da melhor maneira possível e sem falhas.";
			mes "Assim você tem a garantia das melhores indicações possíveis.";
			next;
			mes "[Guia de Izlude]";
			mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa.";
			mes "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no botão 'mapa' no menu principal.";
			mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

izlude,133,113,4	duplicatr(Guia#01izlude)	Guia#02izlude	105
//izlude,120,206,4	duplicatr(Guia#01izlude)	Guia#03izlude	105,{}