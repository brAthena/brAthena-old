//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: niflheim.txt                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.2/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Niflheim.                |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Holy & Protimus]                   |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

niflheim,201,187,3	script#re	Guia de Nifflheim#01nif	849,{
	mes "[Guia de Nifflheim]";
	mes "Bem-vindo a... ^8B4513Niflheim^000000... O Reino dos Mortos...";
	mes "Gostaria de conhecer o reino...?";
	if(F_Navi() == 0) {
		next;
		mes "[Guia de Nifflheim]";
		mes "Quando você clicar no... ^B9062F[nome da localização]^000000... você receberá os mais avançados serviços de ^B9062FNavegação^000000...!";
	}
	while (1) {
		next;
		switch(select("[ Loja de Utilidades ]:[ Bar ]:[ Loja de Armas ]:[ Castelo da Bruxa ]:Remover marcadores do mini-mapa:Cancelar")) {
		case 1:
			callsub L_Mark, F_Navi("Loja de Utilidades","niflheim,218,196","^0000FF");
			viewpoint 1,218,196,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark, F_Navi("Bar","niflheim,189,208","^006400");
			viewpoint 1,189,208,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("Loja de Armas","niflheim,218,170","^800080");
			viewpoint 1,218,170,2,0xDA70D6;
			break;
		case 4:
			callsub L_Mark, F_Navi("Castelo da Bruxa","niflheim,255,195","^FF1493");
			viewpoint 1,255,195,3,0xFF1493;
			break;
		case 5:
			mes "[Guia de Nifflheim]";
			mes "Claro, irei remover todos os pontos marcados de seu mini-mapa...";
			mes "Há mais algo que eu possa fazer por você...?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			break;
		case 6:
			mes "[Guia de Nifflheim]";
			mes "......Adeus~!..";
			close;
		}
	}
	end;
L_Mark:
	mes "[Guia de Nifflheim]";
	mes "Deixe-me marcar para você a localização para "+getarg(0)+"^000000 em seu mini-mapa.";
	mes "Gostaria de verificar outras localizações?";
	return;
}

niflheim,107,156,6	script#pre	Homem Nervoso#nif	798,{
	mes "[Lee karl]";
	mes "Por que você veio a um lugar tão horrível como este?";
	mes "Acho que você acabou se perdendo, isso sim.";
	mes "Eu fui atraído pelas altas árvores deste lugar.";
	mes "Demorou um bom tempo até eu perceber que eu estava perdido.";
	mes "E que não havia mais volta.";
	next;
	mes "[Lee karl]";
	mes "Eu já procurei pela saída em todos os lugares.";
	mes "Por isso, estou bem acostumado com o lugar.";
	mes "Lembrar das localidades por aqui pode significar a diferença entre a vida e a morte.";
	next;
	switch(select("Perguntar sobre a cidade.:Limpar indicações do minimapa.:Cancelar.")) {
	case 1:
		mes "[Lee Karl]";
		mes "Então, onde você quer ir?";
		next;
		switch(select("Castelo da Feiticeira.:Loja de Útilidades.:Loja de Armas.:Hotel.:Cancelar.")) {
		case 1:
			mes "[Lee karl]";
			mes "Uma feiticeira medonha vive em Nifflheim.";
			mes "^FF3355+^000000O castelo dela é logo ali.";
			mes "Se você sentir um calafrio na espinha, saia correndo!";
			viewpoint 1,253,191,2,0xFF3355;
			break;
		case 2:
			mes "[Lee Karl]";
			mes "A loja de utilidades é bem ali ^CE6300+^000000.";
			mes "Existem alguns itens que você não pode comprar no mundo dos vivos. Confira.";
			Emotion e_wah;
			viewpoint 1,217,196,3,0xCE6300;
			break;
		case 3:
			mes "[Lee karl]";
			mes "A loja de armas é por ali ^55FF33+^000000.";
			mes "Existem algumas armas que você não pode comprar no mundo dos vivos. Confira.";
			Emotion e_wah;
			viewpoint 1,216,171,4,0x55FF33;
			break;
		case 4:
			mes "[Lee Karl]";
			mes "O hotel fica ali ^3355FF+^000000, as pessoas mortas costumam fazer festas por lá.";
			mes "Mexa o esqueleto!";
			viewpoint 1,189,207,5,0x3355FF;
			break;
		case 5:
			mes "[Lee Karl]";
			mes "Se você quiser apagar todas as indicações no seu mapa, selecione a opção 'Limpar indicações do minimapa'";
		}
		break;
	case 2:
		viewpoint 2,253,191,2,0xFF3355;
		viewpoint 2,217,196,3,0xCE6300;
		viewpoint 2,216,171,4,0x55FF33;
		viewpoint 2,189,207,5,0x3355FF;
		mes "[Lee Karl]";
		mes "Apaguei todas as indicações do seu mapa.";
		mes "Volte se quiser que eu indique as direções novamente.";
		break;
	case 3:
		mes "[Lee karl]";
		mes "Tome cuidado, algumas coisas parecem mortas mas estão bem vivas...";
		mes "Boa sorte!";
		break;
	}
	close;
}