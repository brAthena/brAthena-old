//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: veins.txt                              |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.1/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Veins.                   |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Holy & Protimus]                   |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

veins,210,345,5	script	Guia#veins01	934,{
	mes "[Guia]";
	if(checkversion(0)) {
		mes "Obrigado por surgir em meio a toda essa areia que existe aqui. Bem-vindo a ^8B4513Veins^000000, o Vilarejo do Defiladeiro.\nSe esta é a primeira vez aqui, por que não utiliza meus serviços primeiro?";
		next;
		while (1) {
			switch(select("[ Templo ]:[ Hotel ]:[ Bar ]:[ Loja de Armas ]:[ Loja de Utilidades ]:[ Aeroporto ]:[ Instituto de Geologia & Pesquisa ]:[ Corporação de Eventos Incríveis ]:Remover marcadores do mini-mapa:Cancelar")) {
			case 1:
				mes "[Guia]"+"\nO templo está sempre cheio de oração dos fiéis que oram pela encarnação da Deusa Freya.\nEle está localizado ao lado norte de Veins.\nGostaria de verificar outras localizações?";
				viewpoint 1,197,256,0,0xFF0000;
				next;
				break;
			case 2:
				mes "[Guia]"+"\nVocê pode repousar de sua viagem no Hotel. O Hotel está localizado no lado Norte-Oeste de Veins.\nGostaria de verificar outras localizações?";
				viewpoint 1,110,278,1,0xFF4500;
				next;
				break;
			case 3:
				mes "[Guia]"+"\nSe você quiser falar com os Cidadãos de Veins, visite o Hotel de Veins. O bar está localizado no lado oeste de Veins.\nGostaria de verificar outras localizações?";
				viewpoint 1,150,215,2,0xDAA520;
				next;
				break;
			case 4:
				mes "[Guia]"+"\nVocê tem que ser capaz de proteger seu próprio corpo.\nA loja de Armas de Veins está vendendo ótimas armas por preços razoáveis..\nEla está localizada ao lado Oeste de Veins.\nGostaria de verificar outras localizações?";
				viewpoint 1,150,175,3,0x008000;
				next;
				break;
			case 5:
				mes "[Guia]"+"\nItens essenciais! Conheça a Loja de Utilidades no Centro da Cidade..\nGostaria de verificar outras localizações?";
				viewpoint 1,230,161,4,0x0000FF;
				next;
				break;
			case 6:
				mes "[Guia]"+"\nO Aeroporto de Veins é atualmente usado apenas para Cargas.\nGostaria de verificar outras localizações?";
				viewpoint 1,273,287,5,0x000080;
				next;
				break;
			case 7:
				mes "[Guia]"+"\nVocê gosta de Geologia?\nSe sim, por favor viste o Instituto de Geologia & Pesquisa Geological no segundp andar da Loja de Armas.\nGostaria de verificar outras localizações?";
				viewpoint 1,150,175,6,0x800080;
				next;
				break;
			case 8:
				mes "[Guia]"+"\nDeixe-me marcar para você a localização da ^808080Corporação de Eventos Incríveis^000000 em seu mini-mapa.\nGostaria de verificar outras localizações?";
				viewpoint 1,209,127,7,0x808080;
				next;
				break;
			case 9:
				mes "[Guia]"+"\nClaro, irei remover todos os pontos marcados de seu mini-mapa.\nHá mais algo que eu possa fazer por você?";
				viewpoint 2,197,256,0,0xFFFFFF;
				viewpoint 2,110,278,1,0xFFFFFF;
				viewpoint 2,150,215,2,0xFFFFFF;
				viewpoint 2,150,175,3,0xFFFFFF;
				viewpoint 2,230,161,4,0xFFFFFF;
				viewpoint 2,273,287,5,0xFFFFFF;
				viewpoint 2,150,175,6,0xFFFFFF;
				viewpoint 2,209,127,7,0xFFFFFF;
				next;
				break;
			case 10:
				mes "[Guia]"+"\nAproveite sua estadia!";
				close;
			}
		}
	} else {
		mes "A Cidade Desértica de Veins dá as boas-vindas aos aventureiros que buscam abrigo das terríveis tempestades de areia.\nSe essa é a primeira vez que você usa nossos Serviços de guia, por que não consulta antes o menu ''Aviso''?";
		set .@loop1,1;
		while(.@loop1) {
			next;
			switch(select("Indicações:Limpar as indicações do minimapa:Aviso:Cancelar")) {
			case 1:
				mes "[Guia]"+"\nEu posso dizer a você onde fica cada edifício que procura, desde que ele fique em Veins.\nE então, aonde você gostaria de ir?";
				if (.@compass_check == 0) {
					mes "Você gostaria que eu";
					mes "indicasse os locais";
					mes "em seu Minimapa?";
					next;
					if (select("Sim.:Não") == 1) set .@compass_check,1;
				}
				set .@loop2,1;
				while(.@loop2) {
					if (.@wait_button_chk == 0) set .@wait_button_chk,1;
					else next;
		
					switch(select("Templo.:Pousada.:Loja de Armas.:Loja de Utilidades.:Aeroplano.:Taverna.:Instituto de Pesquisas Geológicas.:Cancelar")) {
					case 1:
						mes "[Guia]"+"\nNosso templo fica ao norte, e está sempre lotado com seguidores fiéis da Deusa Freya.";
						if (.@compass_check == 1) viewpoint 1,196,258,1,0xFF0000;
						break;
					case 2:
						mes "[Guia]"+"\nVocê pode descansar de sua viagem na Pousada. O edifício à minha esquerda é a Pousada de Rachel.";
						if (.@compass_check == 1) viewpoint 1,128,266,2,0xFF00FF;
						break;
					case 3:
						mes "[Guia]"+"\nSim, você precisa se proteger do perigo, não é?\nCompre armas de alta qualidade a preços acessíveis na Loja de Armas de Veins, a oeste.";
						if (.@compass_check == 1) viewpoint 1,150,175,3,0x99FFFF;
						break;
					case 4:
						mes "[Guia]"+"\nVocê tem tudo o que precisa para suas aventuras?\nSe não tem, sugiro que você veja o que a Loja de Utilidades de Veins, no centro, tem a oferecer.";
						if (.@compass_check == 1) viewpoint 1,230,161,4,0x0000FF;
						break;
					case 5:
						mes "[Guia]"+"\nLembre-se que Veins só opera com aeroplanos de carga.";
						if (.@compass_check == 1) viewpoint 1,273,285,5,0x00FF00;
						break;
					case 6:
						mes "[Guia]"+"\nSe quiser fazer amizade com o povo daqui, sugiro que você vá tomar uma bebida na taverna a oeste.";
						if (.@compass_check == 1) viewpoint 1,150,217,6,0x00FF00;
						break;
					case 7:
						mes "[Guia]"+"\nEstá interessado em estudar geologia?\nEntão você deve ir ao instituto de pesquisas geológicas, no segundo andar da Loja de Armas.";
						if (.@compass_check == 1) viewpoint 1,150,175,7,0x00FF00;
						break;
					case 8:
						mes "[Guia]"+"\nSe você quiser remover todas essas marcas de localização de seu mapa, é só falar comigo de novo, e escolher 'Limpar as indicações do Minimapa'.";
						set .@loop2,0;
						break;
					}
				}
				break;
		
			case 2:
				viewpoint 2,196,258,1,0xFF0000;
				viewpoint 2,128,266,2,0xFF00FF;
				viewpoint 2,150,175,3,0x99FFFF;
				viewpoint 2,230,161,4,0x0000FF;
				viewpoint 2,273,285,5,0x00FF00;
				viewpoint 2,150,217,6,0x00FF00;
				viewpoint 2,150,175,7,0x00FF00;
				mes "[Guia]"+"\nPronto, elas já sumiram. Se você quiser informações sobre outros lugares, é só falar comigo.\nAproveite sua estada em Veins.";
				break;
			case 3:
				mes "[Guia]"+"\nQuando usar o menu 'Guia da Vila', lembre-se que a posição dos lugares que você procura será marcada em seu mapa, no canto superior direito da tela.\nSe você não estiver vendo seu mapa, use a tecla de atalho ''ctrl+tab'' ou pressione o botão ''mapa'' em sua janela de informações básicas, tudo bem?\nE você também pode aproximar a visão de seu mapa usando o botão '-' caso você não consiga visualizar o mapa da vila inteiro.";
				break;
			case 4:
				mes "[Guia]"+"\nAproveite sua estada em Veins.";
				close2;
				set .@loop1,0;
				break;
			}
		}
		close;
	}
}

veins,189,101,5	duplicate(Guia#veins01)	Guia#veins02	934