//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: tierra01.txt                           |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.3/1.0/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: NPC's de Entrada de Tierra.                 |
//   | - Oficiais do Desfiladeiro de Tierra- Lado Guillaume   |
//   | - Cavaleiros                                           |
//   | - Oficiais do Desfiladeiro de Tierra- Lado Guillaume   |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [Tyler]                             |
//   | 1.2 Otimização [Holy & Hold]                           |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Oficiais do Desfiladeiro de Tierra- Lado Guillaume
//============================================================
bat_room,124,178,5	script	Oficial do Desfiladeiro#01a	418,{

	set .@npcbgt$,((strnpcinfo(2) == "01a" || strnpcinfo(2) == "02a") ? "Guillaume":"Croix");
	set .@npcbgt2$,((strnpcinfo(2) == "01b" || strnpcinfo(2) == "02b") ? "Guillaume":"Croix");
	set .@npcbgt3, ((strnpcinfo(2) == "01a" || strnpcinfo(2) == "01b") ? 1:2);
	mes "[Oficial do Exército "+.@npcbgt$+"]";
	if (checkweight(1201,1) == 0) {
		mes "- Espere aí um minuto! -";
		mes "- Atualmente você está -";
		mes "- carregando itens demais. -";
		mes "- Por favor tente novamente após -";
		mes "- você descarregar um pouco do peso. -";
		close;
	}
	mes "Vamos mostrar todo o poder do Exército de "+.@npcbgt$+" para aqueles "+.@npcbgt2$+"s fedorentos!";
	next;
	if(select("Eu quero me juntar ao seu exército!:Terminar a conversa.") == 2) {
		mes "[Oficial do Exército "+.@npcbgt$+"]";
		mes "Hoje nós sairemos vencedores!";
		close;
	}
	if ((Class == job_Novice) || (BaseClass == Job_Supernovice) || BaseLevel < 80) {
		mes "[Oficial do Exército "+.@npcbgt$+"]";
		mes "Eu estou muito honrado que você queira se juntar para lutar contra o exército "+.@npcbgt2$+" mas, eu sinto muito!: Eu não posso mandar um novato como você para morrer cruelmente no campo de batalha.";
	}
	else {
		set .@chk_urtime,checkquest(2069,PLAYTIME);
		if ((.@chk_urtime == 0) || (strnpcinfo(0) == "Oficial do Desfiladeiro#01a") && (.@chk_urtime == 1)) {
			mes "[Oficial do Exército "+.@npcbgt$+"]";
			mes "Me parece que você acabou de voltar do campo de batalha.";
			mes "Então é muito cedo pra você retornar. Vá descansar um pouco e deixe os inimigos conosco!";
		}
		else {
			if (.@chk_urtime == 2)
				erasequest 2069;
			set .@mapcount,getmapusers("bat_a0"+((.@npcbgt3 == 1) ? "1":"2"));
			if (.@mapcount > 0) {
				mes "[Oficial do Exército "+.@npcbgt$+"]";
				mes "Eu recebi um comunidado informando que uma unidade do batalhão já foi despachada para o campo de batalha e está enfrentando os "+.@npcbgt2$+"s no momento.";
				mes "Por que você não espera um instante? Eu aconselho você a afiar suas armas e preparar seus suprimentos até a hora da batalha.";
			}
			else {
				if ( (.@npcbgt3 == 1) && $@TierraBG1 || (.@npcbgt3 == 2) && $@TierraBG2) {
					mes "[Oficial do Exército "+.@npcbgt$+"]";
					mes "Uma unidade do batalhão já está preparada e esperando ser despachada ao campo de batalha.";
					mes "Por que você não espera um instante? Eu aconselho você a afiar suas armas e preparar seus suprimentos até a hora da batalha.";
				}
				else {
					mes "[Oficial do Exército "+.@npcbgt$+"]";
					mes "Certamente você já está preparado para a batalha!";
					mes "Vá e mostre aos "+.@npcbgt2$+"s o significado da palavra medo!";
					mes "Hoje nosso grito de vitória deverá ser entoado por todo campo de batalha!";
					close2;
					warp "bat_room",((.@npcbgt3 == 1) ? 57 : 114),((.@npcbgt$ == "Guillaume") ? 223 : 207);
					end;
				}
			}
		}
	}
	close;
}

//============================================================
bat_room,140,178,5	duplicate(Oficial do Desfiladeiro#01a)	Oficial do Desfiladeiro#02a	418
bat_room,125,121,1	duplicate(Oficial do Desfiladeiro#01a)	Oficial do Desfiladeiro#01b	414
bat_room,140,121,1	duplicate(Oficial do Desfiladeiro#01a)	Oficial do Desfiladeiro#02b	414
//============================================================

bat_room,127,178,5	script	Cavaleiro Guillaume#1	417,{
	mes "["+strnpcinfo(1)+"]";
	mes "O Desfiladeiro de Tierra consiste em uma divisão de dois íngremes lados dispostos verticalmente e divididos horizontalmente e, em cada um dos lados há um depósito de suprimentos, tanto para os Guillaumes quanto para os Croixs nas direções Noroeste e Sudoeste respectivamente.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "A batalha começa dentro do navio de seu exército e, o objetivo é avançar e destruir todos os suprimentos do depósito inimigo antes que ele destrua o seu.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "O curandeiro do campo de batalha irá ressuscitar os soldados a cada 25 segundos para que eles possam voltar a batalhar.";
	mes "O exército que capturar a bandeira neutra no centro do Desfiladeiro será recompensado com pontos de regeneração extras, significando que seus soldados irão ser ressuscitados mais que os do outro lado, dando assim alguma vantagem.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Estes dois depósitos de suprimentos estão conectados um ao outro por pequenos e estreitos caminhos mas, os portões estão bloqueados com barricadas.";
	mes "Pense e se mova o mais rápido que pode. A vitória do exército aliado depende muito de sua contribuição.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Por favor, lembre-se: quando você entra em uma batalha você receberá um sinal que indica o período de duração pelo qual você não poderá participar de um mesmo tipo de batalha.";
	mes "Você poderá checar esta duração pressionando os botões Alt+U.";
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Você está pronto pra batalha? Então se apresente ao recrutador próximo à mim!";
	close;
}

bat_room,143,178,5	duplicate(Cavaleiro Guillaume#1)	Cavaleiro Guillaume#2	417
bat_room,127,121,1	duplicate(Cavaleiro Guillaume#1)	Cavaleiro Croix#1	413
bat_room,143,121,1	duplicate(Cavaleiro Guillaume#1)	Cavaleiro Croix#2	413