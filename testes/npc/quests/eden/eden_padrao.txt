//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: eden_padrao.txt                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: ??/1.6/1.1                          |
//   |--------------------------------------------------------|
//   | Descrição: NPC's do grupo Éden.                        |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Tradução PT-BR [lututuiBR]                         |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

moc_para01,27,35,5	script	Secretária Lime Evenor	952,{

	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Espere um minuto !! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você -";
		mes "- Por favor, tente novamente -";
		mes "- depois de se livrar de alguns deles. -";
		close;
	}
	mes "[Lime Evenor]";
	mes "Pessoas que seguem seus sonhos e romances, me escutem. Nós somos os representantes do paraíso conhecido como grupo Éden. Ele é como o 'Jardim do Éden'. Não há nenhum outro lugar como esse.";
	mes "Olá. Posso ajudar?";
	next;
	while (1) {
		switch (select("O que é o grupo Éden?:Participar do Grupo Éden.:Registrar nova missão.:Buscar missões.:Cancelar.")) {
		case 1:
			mes "[Lime Evenor]";
			mes "O Grupo Éden é um grupo que ajuda pessoas que passam por alguma situação difícil em Rune-Midgard.";
			next;
			mes "[Lime Evenor]";
			mes "Se alguém precisa de ajuda, publicamos o caso no quadro de missões e o apresentamos aos nossos membros que queiram ajudar.";
			next;
			mes "[Lime Evenor]";
			mes "Os membros do Éden analisarão as missões e ajudarão aqueles que as publicaram.";
			mes "Os membros cumprem essas missões em troca de recompensas.";
			next;
			mes "[Lime Evenor]";
			mes "Se você não está muito familiarizado com Rune-Midgard, essa é sua grande chance de explorá-lo.";
			mes "Ou, se for madur"+((Sex) ? "o" : "a")+" o suficiente, você pode viajar pelo mundo e fazer novos amigos, enquanto realiza as missões. Seu honrável nome será espalhado pelo mundo.";
			next;
			break;
		case 2:
			if (countitem(6219) < 1) {
				mes "[Lime Evenor]";
				mes "Você pode ser um mebro do Éden simplismente se registrando comigo.";
				mes "Você gostaria de participar do Grupo Éden?";
				next;
				switch (select("Sim, eu quero.:Não, eu não quero.")) {
				case 1:
					mes "[Lime Evenor]";
					mes "Certo. Excelente! Por favor, escreva seu nome aqui.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Terminou?";
					mes "Deixe-me ver.";
					next;
					mes "[Lime Evenor]";
					mes "Seu nome é ^3131FFBo"+.@input$+"Ba^000000..? Isso está certo?";
					mes "Hã? Não??";
					next;
					emotion e_swt;
					mes "[Lime Evenor]";
					mes "Hmm, não é o que você escreveu?";
					mes "Ok, parece um pouco difícil de ler.";
					next;
					mes "[Lime Evenor]";
					mes "Você poderia escrever seu nome denovo? Por favor, escreva-o ^3131FFclaramente^000000.";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "Ah~ ha. Você é ^3131FF"+strcharinfo(0)+"^000000.";
					mes "Eu consegui entender dessa vez.";
					next;
					emotion e_no1;
					mes "[Lime Evenor]";
					mes "Já colocamos seu nome na lista.";
					mes "Bem-vindo, novo membro do Grupo Éden!";
					next;
					mes "[Lime Evenor]";
					mes "Esperamos que você possa fazer um grande trabalho como membro do Éden.";
					getitem 6219,1; //Para_Team_Mark
					next;
					break;
				case 2:
					mes "[Lime Evenor]";
					mes "Você ainda tem dúvidas sobre o Grupo Éden?";
					next;
					break;
				}
			} else {
				mes "[Lime Evenor]";
				mes "Você já é um membro do grupo Éden.";
				next;
			}
			break;
		case 3:
			mes "[Lime Evenor]";
			mes "Você quer registrar algumas missões que deseja cumpri-las?";
			next;
			if(select("Sim, eu quero registrá-las.:Não, eu não quero.") == 1) {
				mes "[Lime Evenor]";
				mes "Certo. Por favor, escreva seu nome.";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000... É este seu nome?";
				mes "Hmm, sua caligrafia não está clara. Eu não consigo entender.";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF"+strcharinfo(0)+"^000000, me diga o que você que registrar";
				mes "Para que mapa devemos ir?";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMapa da Missão: "+.@inputstr$+"^000000";
				mes "Hum, vou anotar isso.";
				next;
				mes "[Lime Evenor]";
				mes "Me diga o tipo de missão você está procurando";
				mes "Seja bem dirOficial do Éden#0, como 'Caçar 10 Porings'.";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "Terminou?";
				mes "Deixe-me ver.";
				next;
				mes "[Lime Evenor]";
				mes "Huh...........";
				next;
				mes "[Lime Evenor]";
				mes "Hum.. hã??..................";
				next;
				emotion e_dots;
				mes "[Lime Evenor]";
				mes "Eu acho que você deve completar uma das nossas missões antes de começar outra.";
				next;
				emotion e_pif;
				mes "[Lime Evenor]";
				mes "^3131FFCliente: Lime Evenor^000000";
				mes "^3131FFMissão: Praticar sua caligrafia por um mês.^000000";
				mes "Sua letra é horrível!";
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMissão:"+.@input$+"^000000";
				mes "De qualquer jeito, você já registrou uma nova missão.";
				next;
				mes "[Lime Evenor]";
				mes "As missões estão lotadas. Você tem que esperar abrir uma vaga.";
				mes "Por favor, espere sua vez.";
				next;
			}
			break;
		case 4:
			if (countitem(6219) > 0) {
				mes "[Lime Evenor]";
				mes "Você gostaria de participar de uma missão como membro do Grupo Éden?";
				next;
				mes "[Lime Evenor]";
				mes "Confira os quadros de missões, na lateral direita, há vários tipos de missões esperando por você.";
				mes "Por favor, leia cuidadosamente a lista de missões e as qualificações, após isso escolha uma delas.";
				next;
			}
			else {
				mes "[Lime Evenor]";
				mes "Primeiro você precisa se registrar no Grupo Éden se quiser algumas missões.";
				close;
			}
			break;
		case 5:
			mes "[Lime Evenor]";
			mes "Se tiver alguma pergunta, volte aqui.";
			close;
		}
	}
	close;
}

prontera,124,76,3	script	Oficial do Éden#0	729,{
	mes "[Oficial de Teleporte do Éden]";
	mes "Aqueles que estão vagando por este mundo sem um objetivo vêm até mim para ouvir o que tenho a dizer!";
	mes "O grupo Éden está aqui para ajudar você e lhe mostrar o futuro.";
	next;
	mes "[Oficial de Teleporte do Éden]";
	mes "Você não tem nada para fazer além de desperdiçar seu tempo?";
	mes "Está ansioso para fazer o bem mas ninguém lhe oferece trabalho?";
	mes "Você gostaria de solucionar problemas?";
	mes "O grupo Éden está aqui para resolver o seu problema.";
	next;
	switch (select("Ir para o Grupo Éden.:Não quero conversar mais.")) {
	case 1:
		mes "[Oficial de Teleporte do Éden]";
		mes "Vamos à nossa base secreta!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Oficial de Teleporte do Éden]";
		mes "Se não souber o que fazer em seu futuro, não hesite em falar comigo. É só passar aqui e falar comigo! Ok?";
		close;
	}
}

moc_ruins,68,164,3	duplicate(Oficial do Éden#0)	Oficial do Éden#2	729
geffen,132,66,3	duplicate(Oficial do Éden#0)	Oficial do Éden#3	729
alberta,124,67,3	duplicate(Oficial do Éden#0)	Oficial do Éden#4	729
aldebaran,133,119,5	duplicate(Oficial do Éden#0)	Oficial do Éden#5	729
izlude,131,148,4	duplicate(Oficial do Éden#0)	Oficial do Éden#6	729
prt_church,103,78,3	duplicate(Oficial do Éden#0)	Oficial do Éden#7	729
geffen_in,160,104,5	duplicate(Oficial do Éden#0)	Oficial do Éden#8	729
moc_prydb1,53,126,3	duplicate(Oficial do Éden#0)	Oficial do Éden#9	729
alberta_in,75,39,3	duplicate(Oficial do Éden#0)	Oficial do Éden#10	729
payon_in02,58,58,1	duplicate(Oficial do Éden#0)	Oficial do Éden#11	729
payon,177,111,3	duplicate(Oficial do Éden#0)	Oficial do Éden#12	729
que_ng,33,73,3	duplicate(Oficial do Éden#0)	Oficial do Éden#13	729
que_ng,144,161,5	duplicate(Oficial do Éden#0)	Oficial do Éden#14	729
yuno,144,189,5	duplicate(Oficial do Éden#0)	Oficial do Éden#15	729
rachel,125,144,3	duplicate(Oficial do Éden#0)	Oficial do Éden#16	729
morocc,161,97,6	duplicate(Oficial do Éden#0)	Oficial do Éden#17	729
comodo,202,151,4	duplicate(Oficial do Éden#0)	Oficial do Éden#18	729
umbala,96,160,3	duplicate(Oficial do Éden#0)	Oficial do Éden#19	729
veins,220,109,4	duplicate(Oficial do Éden#0)	Oficial do Éden#20	729
lighthalzen,164,86,4	duplicate(Oficial do Éden#0)	Oficial do Éden#21	729
einbroch,250,211,4	duplicate(Oficial do Éden#0)	Oficial do Éden#22	729
hugel,93,153,4	duplicate(Oficial do Éden#0)	Oficial do Éden#23	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{

OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: if (checkversion(0)) warp "izlude",134,118; else warp "izlude",127,142; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "morocc",156,93; end;
		case 18: warp "comodo",209,143; end;
		case 19: warp "umbala",99,155; end;
		case 20: warp "veins",216,123; end;
		case 21: warp "lighthalzen",158,92; end;
		case 22: warp "einbroch",245,210; end;
		case 23: warp "hugel",95,145; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (countitem(6219) > 0) {
		mes "[Lime Evenor]";
		mes "Oh, esse é um lugar exclusivo para membros do Grupo Éden.";
		mes "Se você for um membro, você poderá entrar quando quiser!";
		next;
		switch (select("Entrar.:Não entrar.")) {
		case 1:
			mes "Essa porta tem uma bela decoração, mas parece pesada demais,";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Lime Evenor]";
			mes "Bem, se não está interessado...";
			close;
		}
	}
	mes "[Lime Evenor]";
	mes "Oh, esse é um lugar exclusivo para membros do Éden.";
	mes "Se você quiser entrar, você tem que participar do Grupo Éden.";
	close;
}

moc_para01,16,22,7	script	Velho Aventureiro#eden	900,{
	mes "[Velho Aventureiro]";
	mes "Argh, sou forte o suficiente, embora pareça velho! Qual é o nome dela? Lime Evenor? Ela só me ignora como se eu fosse inútil.";
	next;
	mes "[Velho Aventureiro]";
	mes "Vim aqui me juntar ao grupo Éden! Não mereço isso!";
	mes "Você também acha que sou inútil?";
	close;
}

moc_para01,182,48,3	script	Chefe do Éden#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Chefe do Éden]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion e_sob;
	mes "[Chefe do Éden]";
	mes "Oh, droga! Quantos ovos preparei?";
	mes "Aaah!!! Esqueci completamente! Terei que contar de novo!";
	next;
	emotion e_sob;
	mes "[Chefe do Éden]";
	mes "Por que me pediu para fazer isso? Que tipo de missão é essa?!";
	next;
	emotion e_sob;
	mes "[Chefe do Éden]";
	mes "O que você vai fazer com todos esses ovos fritos? Tenho certeza que não consegue comer tudo isso. Você só quer me incomodar, certo?!";
	close;
}

moc_para01,172,28,5	script	Estagiário do Éden#eden	883,{
	mes "[Estagiário do Éden]";
	mes "Na verdade, eu percebi isso.";
	next;
	mes "[Estagiário do Éden]";
	mes "Precisamos nos esforçar muito se não quisermos falhar.";
	next;
	mes "[Estagiário do Éden]";
	mes "Mas, eu não sei nada sobre farmácia ainda.";
	next;
	mes "[Estagiário do Éden]";
	mes "Eu vou falhar. Não posso fazer isso.";
	close;
}

moc_para01,20,35,5	script	Assistente de Escritório#Neede	814,{
	mes "[Neede]";
	mes "Eu me tornei membro do Éden há muitos anos, mas parece que foi ontem.";
	mes "Eu tenho feito muito trabalho, então nem notei quantos anos eu passei aqui.";
	next;
	mes "[Neede]";
	mes "Isso me lembra que todos os dias eram como uma guerra.";
	next;
	mes "[Neede]";
	mes "O chefe ^3131FFdo restaurante à minha direita^000000";
	mes "usa muito tempero quando cozinha, o cheiro se espalha por toda a sala de espera. Nós temos que abrir a janela porque não suportamos o cheiro!";
	next;
	mes "[Neede]";
	mes "E, você está vendo o ^3131FFginásio no lado norte da sala de espera^000000? Eles sempre gritam quando se exercitam, consigo ouvi-los daqui!";
	next;
	emotion e_ag;
	mes "[Neede]";
	mes "Além disso, a garota esquisita ^3131FFdo segundo andar^000000 chora o tempo todo. Isso me deixa louca.";
	next;
	mes "[Neede]";
	mes "Ah, a garota esquisita é, na verdade, nossa chefe...";
	next;
	mes "[Neede]";
	mes "Não sei o que ela anda fazendo. Mas ela chora e grita o tempo todo. Ah, às vezes ouço um 'Bang!', sons estrondosos... Eu não sei de onde está vindo isso.";
	next;
	emotion e_omg;
	mes "[Neede]";
	mes "Espere!";
	mes "Shh! É segredo, você não pode contar para ninguém!";
	next;
	mes "[Neede]";
	emotion e_sigh;
	mes "Não confio em Evenor. Ela nunca mostra seus sentimentos... ela pensava em me dedurar para a chefe.";
	next;
	emotion e_pif,0,"Secretária Lime Evenor";
	mes "[Lime Evenor]";
	mes "Não sou esse tipo de pessoa.";
	next;
	emotion e_wah;
	mes "[Neede]";
	mes "Hã? Você ouviu isso? Caramba!";
	close;
}