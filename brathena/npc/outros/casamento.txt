//    ____________________________________________________________
//   /                                                            \
//   |       _           _   _   _                                |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _           |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |          |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |          |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|          |
//   |                                                            |
//   |                    brAthena Script                         |
//   |------------------------------------------------------------|
//   | Nome do Script: casamento.txt                              |
//   |------------------------------------------------------------|
//   | Criado por: rA/brA Dev. Team                               |
//   |------------------------------------------------------------|
//   | Versões rA/brA: 1.4/1.2                                    |
//   |------------------------------------------------------------|
//   | Descrição: Script de casamento oficial.                    |
//   | Adicionado verificação de isloggedin(), mesmo que o        |
//   | Aegis não  Faça isso.                                      |
//   | Atualmente não oferece suporte a casamentos do mesmo sexo. |
//   | Script antigo localizado em: npc/custom/marriage.txt       |
//   | -Variável em uso: wedding_sign (máximo 1)                  |
//   | -Variável em uso: $@wedding  (máximo 1)                    |
//   | -Variável em uso: $@wed_groom$ $@wed_bride$                |
//   |------------------------------------------------------------|
//   | Changelog:                                                 |
//   | 1.0 Criado Cabeçalho [Megasantos]                          |
//   | 1.1 Tradução PT-BR [Clare]                                 |
//   | 1.2 Adicionado função de Administração [lututuiBR]         |
//   |------------------------------------------------------------|
//   | - Anotações                                                |
//   \____________________________________________________________/

prt_church,97,100,4	script	Wedding Staff#w	1_F_LIBRARYGIRL,{
	cutin "wedding_marry01",2;
	if (Upper == 2) {
		mes "[Marry Happy]";
		mes "Olá!";
		mes "Meu nome é Marry Happy";
		mes "e eu estou aqui para lhe fornecer";
		mes "mais informações sobre o";
		mes "casamento. Agora, diga para mim";
		mes "qual é a sua dúvida?";
		next;
		if (select("Eu quero me casar.:Eu não preciso de sua ajuda!") == 1) {
			mes "[Marry Happy]";
			mes "Oh, me deculpe, mas";
			mes "personagens adotados";
			mes "não possuem permissão para";
			mes "casar. Por agora, porque";
			mes "você não aprecia o simples";
			mes "prazer da sua infância?";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "Oh, é claro que você não";
		mes "pode! Pequenas crianças";
		mes "não podem se casar --";
		mes "existem várias leis sobre";
		mes "isso ~ você não é apenas";
		mes "uma criança adorável?";
		goto L_End;
	}
	mes "[Marry Happy]";
	mes "O casamento é a bela união de";
	mes "duas almas que decidiram";
	mes "ficar juntas para sempre,";
	mes "para compartilhar suas alegrias e vidas.";
	mes "Então há alguém especial,";
	mes "que você gosta, em sua vida?";
	next;
	switch(select("Perguntar sobre a cerimônia de casamento:Perguntar sobre os procedimentos:Preparar-se para o casamento:Juntos somos invencíveis como um exército!")) {
	case 1:
		mes "[Marry Happy]";
		mes "O sábio e belevolente";
		mes "Rei Tristram III era quem";
		mes "fazia as cerimônias de casamento,";
		mes "mas por causa de seus deveres reais";
		mes "ele não pode mais fazer isso.";
		mes "Ele possui uma agenda lotada.";
		next;
		mes "[Marry Happy]";
		mes "Bispo Vomars, o bispo";
		mes "do amor, é agora oficialmente";
		mes "quem realiza as cerimônias de casamento.";
		mes "Ele é um verdadeiro tesouro do";
		mes "reino de Rune-Midgarts.";
		next;
		mes "[Marry Happy]";
		mes "Quando você se casa com alguém,";
		mes "é para o resto da vida, por isso pense bem antes de fazer ou";
		mes "aceitar uma proposta.";
		mes "Tenha em mente que um homem";
		mes "só pode se casar com uma mulher e vice versa.";
		next;
		mes "[Marry Happy]";
		mes "Se você tiver a sorte de encontrar";
		mes "alguém que você realmente queira";
		mes "passar o resto de sua vida junto,";
		mes "então você deve fazer a proposta. Eu espero que todo mundo";
		mes "encontre o seu par perfeito...";
		break;

	case 2:
		mes "[Marry Happy]";
		mes "A primeira parte dos";
		mes "procedimentos para o casamento";
		mes "é completar o pedido.";
		mes "Uma vez que o Noivo e a Noiva";
		mes "aceitarem o pedido, eles devem formar";
		mes "um grupo juntos, com apenas duas pessoas.";
		next;
		mes "[Marry Happy]";
		mes "Após formar o grupo,";
		mes "o casal deve então falar com o ";
		mes "Bispo Vomars. O noivo deve";
		mes "falar primeiro e dizer ao bispo o nome";
		mes "exato de sua noiva. Caso contrário, a cerimônia não será realizada.";
		next;
		mes "[Marry Happy]";
		mes "Depois, a noiva deverá";
		mes "falar com o Bispo e dizer";
		mes "para ele o nome exato do seu noivo.";
		mes "Se os nomes forem ditos corretamente";
		mes "ao Bispo, eles serão capazes";
		mes "de trocar alianças.";
		next;
		mes "[Marry Happy]";
		mes "Uma vez que as alianças são trocadas,";
		mes "o casal está sempre ligado, em matrimônio é claro. Bem,";
		mes "antes deste ponto, há muitas chances de mudar de idéia, então...";
		next;
		mes "[Marry Happy]";
		mes "Se houver muitos casais";
		mes "para se casar, de uma única vez";
		mes "porfavor, fomem uma linha";
		mes "e falem com o Bispo";
		mes "Vomars um de cada vez, pois só é possível fazer um casamento por vez.";
		next;
		mes "[Marry Happy]";
		mes "E por ultimo, não esqueça de dizer";
		mes "ao Bispo Vomars o nome exato";
		mes "do seu parceiro, sem perder muito tempo. Se você demorar muito para dizer,";
		mes "a cerimônia será automaticamente cancelada e você terá de tentar fazer novamente mais tarde.";
		next;
		mes "[Marry Happy]";
		mes "As noivas precisam lembrar-se";
		mes "que só têm 3 minutos para terminar de falar";
		mes "com o bispo Bispo";
		mes "Vomars após o sue parceiro";
		mes "ter terminado de falar com ele.";
		next;
		if (select("Obrigado, isso ajudou muito!:Qual a maneira mais fácil de dizer o nome do meu parceiro?") == 1) {
			mes "[Marry Happy]";
			mes "Bem, eu estou aqui para ajudar";
			mes "os casais a se prepararem";
			mes "da melhor forma possível.";
			mes "Se tem algo que";
			mes "você não entendeu,";
			mes "fique a vontade para perguntar novamente.";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "A maneira mais fácil de";
		mes "dizer o nome do seu parceiro para";
		mes "o bispo é enviar uma mensagem privada";
		mes "para seu parceiro, e";
		mes "então clicar na seção de nome que está à esquerda do bate-papo.";
		next;
		mes "[Marry Happy]";
		mes "Após isso precione as teclas ''Ctrl'' e ''C''";
		mes "para copiar o nome. Então,";
		mes "você poderá colar o nome";
		mes "na hora de digitar, apertando as teclas";
		mes "''Insert'' e ''Shift''. Isso é bem fácil, certo?";
		next;
		mes "[Marry Happy]";
		mes "Tudo bem, Agora vamos tentar";
		mes "isso. Praticaremos isso com";
		mes "você me dando o nome do seu parceiro";
		mes "usando esse metodo.";
		next;
		input .@partner$;
		mes "[Marry Happy]";
		mes "Tudo bem, depois que decidirem";
		mes "se casar,";
		mes "volte até mim e";
		mes "e me fale sobre isso.";
		mes "Vejo vocês depois, aventureiros!";
		break;

	case 3:
		cutin "wedding_marry02",2;
		if (Sex) {
			mes "[Marry Happy]";
			mes "Então você gostaria de se casar?";
			mes "Como um noivo, você precisa se preparar, você precisa de";
			mes "^3377FF1 Terno^000000 e pagar ^3377FF1,300,000 zeny^000000.";
			mes "Noivas precisam de seus próprios vestidos e pagar uma taxa";
			mes "de 1,200,000 zeny.";
		}
		else {
			mes "[Marry Happy]";
			mes "Então você gostaria de se casar?";
			mes "Como uma noiva, você precisa se preparar, você precisa de";
			mes "1 vestido de casamento e pagar uma taxa";
			mes "de 1,200,000 zeny. Os noivos devem";
			mes "trazer 1 Terno e pagar 1,300,000 zeny para se casarem.";
		}
		next;
		mes "[Marry Happy]";
		mes "Noivos e noivas precisam";
		mes "também de ^3377FF1 Anel de Diamantes^000000, um para cada,";
		mes "para serem trocados com seu parceiro.";
		mes "Você vai precisar de todos esses";
		mes "preparativos prontos quanto você";
		mes "vier para sua cerimônia.";
		next;
		mes "[Marry Happy]";
		mes "Tanto o noivo como a noiva";
		mes "devem cumprir todos os preparativos";
		mes "antes de fazerem a cerimônia";
		mes "de casamento.";
		mes "Agora, Você gostaria de";
		mes "começar a cerimônia de casamento?";
		next;
		if (select("Sim:Não") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "[Marry Happy]";
				mes "Sinto muito, mas eu não posso";
				mes "permitir que você se case novamente!";
				mes "Eu não vou permitir que você traia";
				mes "o seu cônjugue, e";
				mes "além disso, a poligamia não é";
				mes "legal aqui em Rune-Midgarts.";
				break;
			}
			else if (wedding_sign == 1) {
				mes "[Marry Happy]";
				mes "Você já";
				mes "completou o pedido?";
				mes "Hmm, certifique-se de que seu";
				mes "parceiro também terminou os";
				mes "preparativos, e";
				mes "então volte a falar com o Bispo Vomars.";
				break;
			}
			else if (BaseLevel < 45) {
				mes "[Marry Happy]";
				mes "Hmm, você precisa ser";
				mes "forte o suficiente para";
				mes "proteger o que você ama antes";
				mes "de considerar o casamento.";
				mes "Após tornar-se mais forte,";
				mes "volte e fale comigo novamente.";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "[Marry Happy]";
				mes "Hm? Você esqueceu";
				mes "de trazer o Anel de Diamantes";
				mes "para trocar com seu parceiro";
				mes "durante a cerimônia de casamento?";
				mes "Procure-o cuidadosamente e";
				mes "volte até aqui quando encontrá-lo, ok?";
				goto L_End;
			}
			else if (Sex) {
				if (Zeny < 1300000) {
					mes "[Marry Happy]";
					mes "Desculpe mas você não";
					mes "possui 1,300,000 zeny";
					mes "todos os noivos devem pagar";
					mes "para terem a cerimônia de casamento.";
					mes "Você perdeu o seu dinheiro?";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "[Marry Happy]";
					mes "Onde está o seu smoking?";
					mes "Você precisa usá-lo durante";
					mes "a cerimônia de";
					mes "casamento! Procure-o, Traga-o para";
					mes "mim, e finalmente você estará pronto";
					mes "para a cerimônia, ok?";
					goto L_End;
				}
			}
			else if (Sex == 0) {
				if (Zeny < 1200000) {
					mes "[Marry Happy]";
					mes "Desculpe, mas todas as";
					mes "noivas devem pagar 1,200,000";
					mes "zeny, esta é a taxa para";
					mes "fazer a cerimônia de casamento. Você";
					mes "poderia pedir ao seu parceiro";
					mes "para ele lhe ajudar com os fundos?";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "[Marry Happy]";
					mes "Oh querida, você esqueceu";
					mes "o seu vestido de casamento?";
					mes "Apresse-se e encontre-o, e então";
					mes "traga para mim -- você";
					mes "definitivamente precisa";
					mes "dele para a cerimônia!";
					goto L_End;
				}
			}
			mes "[Marry Happy]";
			mes "Bem, parece que você";
			mes "tem tudo pronto.";
			mes "Embora eu não tenha certeza";
			mes "de quem seu parceiro é, deixe-me";
			mes "ser a primeira a felicitá-la";
			mes "pelo seu casamento!";
			next;
			mes "[Marry Happy]";
			mes "Agora, vamos terminar os";
			mes "preparativos. Por favor";
			mes "escreva o seu nome exato aqui.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "[Marry Happy]";
					mes "Hmmm, você deve escrever";
					mes "seu nome exatamente como ele";
					mes "é exibido. Talvez você";
					mes "precise copiar e colar?";
					mes "De qualquer forma, vamos tentar de novo.";
					next;
				}
				else 
					break;
			}
			mes "[Marry Happy]";
			mes "Ótimo, parece que nós terminamos";
			mes "todos os seus preparativos.";
			mes "Lembre-se você precisa dizer";
			mes "ao Bispo Vomars o nome";
			mes "exato do seu parceiro quando";
			mes "você falar com ele depois, ok?";
			next;
			mes "[Marry Happy]";
			mes "Quando seu parceiro terminar os";
			mes "preparativos também,";
			mes "vocês devem falar com o";
			mes "Bispo para começar";
			mes "a cerimônia de casamento.";
			emotion e_lv;
			next;
			mes "[Marry Happy]";
			if (Sex) {
				mes "Já que você é o noivo,";
				mes "você precisa falar com o";
				mes "Bispo primeiro. Quando você";
				mes "terminar, é a vez de sua";
				mes "sua noiva falar com o";
				mes "Bispo Vomars.";
				set Zeny, Zeny-1300000;
				delitem 7170,1; //Tuxedo
			}
			else {
				mes "Como você é a noiva,";
				mes "você precisa esperar o seu";
				mes "noivo falar com o Bispo";
				mes "Vomars primeiro. Quando ele";
				mes "terminar, será sua vez de falar";
				mes "com o Bispo Vomars.";
				set Zeny, Zeny-1200000;
				delitem 2338,1; //Wedding_Dress
			}
			delitem 2613,1; //Diamond_Ring
			set wedding_sign,1;
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "Não...?";
		mes "Bem, quando você estiver";
		mes "pronto para o casamento,";
		mes "fique a vontade para voltar";
		mes "com todo pronto,";
		mes "ok? Tenha um bom dia.";
		break;

	case 4:
		cutin "wedding_marry02",2;
		donpcevent "Soldado Solitário#Prontera::OnEnable";
		donpcevent "Soldado Solitário#Geffen::OnEnable";
		donpcevent "Soldado Solitário#Morocc::OnEnable";
		donpcevent "Soldado Solitário#Payon::OnEnable";
		donpcevent "Soldado Solitário#Amatsu::OnEnable";
		donpcevent "Soldado Solitário#Gonryun::OnEnable";
		emotion e_omg;
		mes "[Soldado Solitário]";
		mes "^CC9933você precisa refinar";
		mes "seus itens para";
		mes "fazer ótimos equipamentos!^000000";
		emotion e_rock,0,"Soldado Solitário#Prontera";
		next;
		mes "[Soldado Solitário]";
		mes "^330099É um desperdicio";
		mes "fazer grupos em";
		mes "cavernas! Eu posso";
		mes "fazer isso sozinho!^000000";
		emotion e_rock,0,"Soldado Solitário#Geffen";
		next;
		mes "[Soldado Solitário]";
		mes "^666666Inferno, eu treinei";
		mes "sempre sozinho, desde o dia";
		mes "que eu nasci, até conseguir";
		mes "minha mudança de classe!^000000";
		emotion e_rock,0,"Soldado Solitário#Morocc";
		next;
		mes "[Soldado Solitário]";
		mes "^666600Eu vou OPTAR por";
		mes "passar o natal sozinho...";
		mes "Sozinho e brincando de";
		mes "palavras cruzadas!^000000";
		emotion e_rock,0,"Soldado Solitário#Payon";
		next;
		mes "[Soldado Solitário]";
		mes "^CC9966Mulheres podem quebrar";
		mes "meu espírito, mas nunca vão";
		mes "tomar... minha LIBERDADE!^000000";
		emotion e_rock,0,"Soldado Solitário#Amatsu";
		next;
		mes "[Soldado Solitário]";
		mes "^669900...Nós somos livres! Nós somos";
		mes "o invencível exercito dos solitários!^000000";
		emotion e_rock,0,"Soldado Solitário#Gonryun";
		close2;
		cutin "wedding_marry01",255;
		emotion e_swt;
		donpcevent "Soldado Solitário#Prontera::OnInit";
		donpcevent "Soldado Solitário#Geffen::OnInit";
		donpcevent "Soldado Solitário#Morocc::OnInit";
		donpcevent "Soldado Solitário#Payon::OnInit";
		donpcevent "Soldado Solitário#Amatsu::OnInit";
		donpcevent "Soldado Solitário#Gonryun::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
}

prt_church,97,102,0	script	Soldado Solitário#Prontera	8W_SOLDIER,{
	mes "[Soldado Solitário]";
	mes "^CC9933você precisa refinar";
	mes "seus itens para";
	mes "fazer ótimos equipamentos!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Prontera";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Prontera";
	emotion e_go;
	end;
}

prt_church,98,102,0	script	Soldado Solitário#Geffen	4_M_GEF_SOLDIER,{
	mes "[Soldado Solitário]";
	mes "^330099É um desperdicio";
	mes "fazer grupos em";
	mes "cavernas! Eu posso";
	mes "fazer isso sozinho!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Geffen";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Geffen";
	emotion e_go;
	end;
}

prt_church,99,102,0	script	Soldado Solitário#Morocc	4_M_MOC_SOLDIER,{
	mes "[Soldado Solitário]";
	mes "^666666Inferno, eu treinei sempre";
	mes "tudo por mim mesmo, desde o";
	mes "dia que eu nasci, até conseguir";
	mes "minha mudança de classe!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Morocc";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Morocc";
	emotion e_go;
	end;
}

prt_church,100,102,0	script	Soldado Solitário#Payon	4_M_PAY_SOLDIER,{
	mes "[Soldado Solitário]";
	mes "^666600Eu vou OPTAR por";
	mes "passar o natal sozinho...";
	mes "Sozinho e brincando de";
	mes "palavras cruzadas!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Payon";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Payon";
	emotion e_go;
	end;
}

prt_church,101,102,0	script	Soldado Solitário#Amatsu	8_M_JPNSOLDIER,{
	mes "[Soldado Solitário]";
	mes "^CC9966Mulheres podem querar";
	mes "meu espírito, mas nunca vão";
	mes "tomar... minha LIBERDADE!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Amatsu";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Amatsu";
	emotion e_go;
	end;
}

prt_church,102,102,0	script	Soldado Solitário#Gonryun	8_M_TWSOLDIER,{
	mes "[Soldado Solitário]";
	mes "^669900...Nós somos livres! Nós somos";
	mes "o invencível exercito dos solitários!^000000";
	close;

OnInit:
	hideonnpc "Soldado Solitário#Gonryun";
	end;

OnEnable:
	hideoffnpc "Soldado Solitário#Gonryun";
	emotion e_go;
	end;
}

prt_church,100,128,4	script	Bishop#w	1_M_PASTOR,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "[Vomars]";
		mes "Saudações, meu pequeno.";
		mes "você está perdido? Hmmm.";
		mes "você sabe onde estão";
		mes "seu papai e sua mamãe?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$@wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex) {
						set $@wedding,1;
						initnpctimer;
						mes "[Vomars]";
						mes "Jovens apaixonados, por favor";
						mes "lembrem-se desse momento";
						mes "pelo resto de suas vidas. Que o futuro de vocês seja abençõado";
						mes "e feliz. Eu desejo que o amor que vocês compartilham";
						mes "cresça ainda mais a cada dia que se passa.";
						next;
						mapannounce "prt_church","Esta é a proposta de casamento do noivo, Sr. " + strcharinfo(0) + "...",bc_map;
						mes "[Vomars]";
						mes "Até o fim do";
						mes "mundo, eu desejo que você";
						mes "fique ao lado dela que é";
						mes "quem você ama, e a apoiar";
						mes "e protegê-la. Agora, poderia";
						mes "dizer o nome de sua noiva?";
						next;
						input $@wed_bride$;
						mes "[Vomars]";
						mes "Mr. " + strcharinfo(0) + "...";
						mes "Você jura por sua vida";
						mes "que você vai sempre amar";
						mes "e cuidar de sua noiva,";
						mes "senhora " + $@wed_bride$ + "?";
						next;
						select("Eu prometo.");
						set $@wed_groom$,strcharinfo(0);
						mes "[Vomars]";
						mes "Agora, é a vez da sua noiva";
						mes "de fazer seus votos";
						mes "de casamento.";
						mes "Dê um passo a frente...";
						close2;
						mapannounce "prt_church","O Noivo, Sr. " + strcharinfo(0) + ", fez seus votos para a Senhora " + $@wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "[Vomars]";
					mes "Sinto muito, mas o noivo";
					mes "deve falar comigo primeiro";
					mes "para começar a cerimônia.";
					mes "Isso é uma moda antiga,";
					mes "mas eu adimito que isso têm";
					mes "feito as coisas funcionarem perfeitamente.";
					goto L_End;
				}
				mes "[Vomars]";
				mes "Antes de se";
				mes "casar, você deve";
				mes "primeiro formar um";
				mes "grupo de dois com seu parceiro.";
				mes "Então, nós poderemos";
				mes "proseguir com a cerimônia.";
				goto L_End;
			}
			mes "[Vomars]";
			mes "Você deve marcar";
			mes "seu casamento primeiro falando com a Happy Marry";
			mes "antes de vir se casar.";
			mes "Happy Marry vai lhe dizer";
			mes "tudo que você precisa fazer";
			mes "para se preparar para o casamento.";
			goto L_End;
		}
		else if ($@wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == 0) {
						if (strcharinfo(0) == $@wed_bride$) {
							mes "[Vomars]";
							mes "Jovens apaixonados, por favor";
							mes "lembrem-se desse momento";
							mes "pelo resto de suas vidas. Que o futuro de vocês seja abençõado";
							mes "e feliz. Eu desejo que o amor que vocês compartilham";
							mes "cresça ainda mais a cada dia que se passa.";
							next;
							mapannounce "prt_church","Vamos ouvir o que a noiva, Senhora "+$@wed_bride$+", tem a dizer sobre a proposta...",bc_map;
							mes "[Vomars]";
							mes "Senhora "+$@wed_bride$+"...";
							mes "Você jura da forma mais";
							mes "verdadeira para "+$@wed_groom$+",";
							mes "que vai ficar ao seu lado, não importa";
							mes "quais os perigos vocês terão de enfrentar?";
							next;
							if (select("^FF0000Não.^000000:Eu Juro.") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Próximo casal, por favor venham...",bc_map;
								mes "[Vomars]";
								mes "Então "+$@wed_groom$;
								mes "você ainda não";
								mes "quer casar? Hmm.";
								mes "Eu realmente sinto muito por esse";
								mes "mal-entendido...";
								set $@wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "[Vomars]";
							mes "Você realmente jura ser";
							mes "fiel e paciente?";
							mes "Você quer mesmo se casar com "+$@wed_groom$+"?";
							next;
							if (select("Sim, eu juro.:^FF0000Não.^000000") == 1) {
								if (isloggedin(getcharid(3,$@wed_groom$))) {
									if (marriage($@wed_groom$)) {
										//Call Wedding effect
										wedding;
										//Give ring to Bride, and change to wedding sprite.
										sc_start SC_WEDDING,3600000,1;
										getitem 2635,1; //Bride_Ring
										//Give ring to Groom, and change to wedding sprite.
										attachrid(getcharid(3,$@wed_groom$));
										sc_start SC_WEDDING,3600000,1;
										getitem 2634,1; //Bridegroom_Ring
										detachrid;
										//Switch Script progression back to Bride
										attachrid(getcharid(3,$@wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","Então eu vos declaro, "+$@wed_groom$+" e "+$@wed_bride$+", marido e mulher.",bc_map;
										mes "[Vomars]";
										mes "Pelos poderes à mim concedidos";
										mes "como o Bispo Real do";
										mes "Reino de Rune-Midgarts,";
										mes "Eu vos declaro marido e";
										mes "mulher. Que seu furuto seja";
										mes "abençoado e com muitas alegrias.";
										next;
										mes "[Vomars]";
										mes "E por último...";
										mes "seja sempre feliz,";
										mes "querida "+$@wed_bride$+"...";
										set $@wed_groom$,"";
										set $@wed_bride$,"";
										set $@wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "[Vomars]";
								mes "Hm. Parece que seu";
								mes "noivo saiu antes que";
								mes "a cerimônia acabasse.";
								mes "Por favor, tente novamente quando ele";
								mes "retornar.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Minha nossa! "+$@wed_bride$+" rejeitou a proposta de "+$@wed_groom$+" para se casarem!",bc_map;
								mes "[Vomars]";
								mes "Hm. Parece que você";
								mes "mudou de idéia no último";
								mes "minuto. Embora eu fique mal com isso";
								mes "irei pedir desculpas ao noivo, você";
								mes "deve sempre fazer o que";
								mes "seu coração diz que é certo. Agora, corra!";
							}
							set $@wed_groom$,"";
							set $@wed_bride$,"";
							set $@wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $@wed_bride$) {
				mes "[Vomars]";
				mes "Hm? Parece que a";
				mes "Happy Marry ainda não";
				mes "recebeu a sua solicitação de";
				mes "casamento. Por favor fale com";
				mes "ela sobre isso primeiro e depois";
				mes "faremos a cerimônia de casamento.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "[Vomars]";
	mes "Eu lhe desejo a felicidade";
	mes "eterna. Nunca abandone";
	mes "seu amado, não importa o quão";
	mes "escuro o presente possa ser, sempre esteja";
	mes "ao seu lado e";
	mes "olhem por um futuro cheio de esperanças.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($@wed_groom$ != "" && $@wed_bride$ != "") {
		mes "[Vomars]";
		mes "O casamento de";
		mes "Senhora "+ $@wed_bride$ +" e";
		mes "do Senhor "+ $@wed_groom$;
		mes "está em andamento.";
		mes "Por favor fique em silêncio.";
		goto L_End;
	}
	mes "[Vomars]";
	mes "Estou fazendo o casamento";
	mes "de outro casal agora, então";
	mes "por favor aguarde por sua";
	mes "vez. Obrigado por compreender... ^FFFFFF ^000000";
	goto L_End;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $@wed_groom$,"";
	set $@wed_bride$,"";
	set $@wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","Você demorou muito a responder... Próximo casal, por favor venham.",bc_map;
	donpcevent "Bishop#w::OnReset";
	stopnpctimer;
	end;
}

prt_church,28,178,4	script	The King of Midgart	1_M_PRON_KING,{
	callfunc "F_GM_NPC";
	mes "[Vomars]";
	mes "Wh-who are you?";
	mes "You must know the";
	mes "password to invoke";
	mes "my awesome powers.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Vomars]";
		mes "This is";
		mes "no place for";
		mes "fooling around.";
		close2;
		warp "prt_church",101,102;
		end;
	}
	mes "[Wedding Switch]";
	mes "Is there a problem with";
	mes "the wedding ceremony?";
	mes "I can reset the Bishop";
	mes "Vomars NPC if you like.";
	next;
	switch(select("No, thanks:RESET")) {
	case 1:
		mes "[Wedding Switch]";
		mes "Alright, then.";
		mes "However, if the";
		mes "Bishop Vomars";
		mes "NPC is stuck, it may";
		mes "be best to reset it.";
		close;
	case 2:
		donpcevent "Bishop#w::OnStop";
		donpcevent "Bishop#w::OnReset";
		mapannounce "prt_church","You've responded too slowly... Next couple, please proceed.",bc_map;
		mes "[Wedding Switch]";
		mes "The Bishop Vomars NPC";
		mes "has now been reactivated.";
		mes "It should now be possible";
		mes "to proceed with weddings.";
		close;
	}
}

prt_church,20,179,4	script	Divorce Staff	1_F_LIBRARYGIRL,{
	callfunc "F_GM_NPC";
	mes "[Bad Ending]";
	mes "Uh oh...";
	mes "You know I can't";
	mes "do anything for you.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Bad Ending]";
		mes "Hmm...";
		mes "You really";
		mes "shouldn't be";
		mes "in this place...";
		close;
	}
	mes "[Bad Ending]";
	mes "Great, you know the";
	mes "password! Now, did you";
	mes "want me to remove the";
	mes "Wedding Ring in your";
	mes "inventory?";
	next;
	switch(select("Drop 1 Wedding Ring.:Keep it.")) {
	case 1:
		set .@ring, (Sex)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring)) {
			delitem .@ring,1;
			mes "[Bad Ending]";
			mes "It's done!";
		} else {
			mes "[Bad Ending]";
			mes "I couldn't find";
			mes "the Wedding Ring...";
			mes "Please make sure";
			mes "that it's not equipped.";
		}
		close;
	case 2:
		mes "[Bad Ending]";
		mes "You sure you want";
		mes "to keep that ring?";
		mes "Alright, but if it becomes";
		mes "a problem, you come to me.";
		close;
	}
}

prt_church,22,179,4	script	Remarry Staff	1_F_LIBRARYGIRL,{
	callfunc "F_GM_NPC";
	mes "[Wedding Again]";
	mes "Hmm...?";
	mes "What exactly are";
	mes "you doing here?";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Wedding Again]";
		mes "Ahk!";
		mes "An adventurer";
		mes "like you shouldn't";
		mes "be in this place!";
		close;
	}
	if (getpartnerid()) {
		mes "[Wedding Again]";
		mes "Hmm...";
		mes "I can only create";
		mes "a Wedding Ring if the";
		mes "character is married.";
		close;
	}
	mes "[Wedding Again]";
	mes "Ah, I see that you";
	mes "know the password.";
	mes "Alright, if you somehow";
	mes "lost your Wedding Ring,";
	mes "I can make you a new one.";
	next;
	switch(select("Make new Wedding Ring.:Cancel.")) {
	case 1:
		set .@ring, (Sex)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring) || isequipped(.@ring)) {
			mes "[Wedding Again]";
			mes "Wait, wait...";
			mes "You're wearing your";
			mes "Wedding Ring. I better";
			mes "not make you another since";
			mes "you don't need more than one.";
		} else {
			getitem .@ring,1;
			mes "[Wedding Again]";
			mes "Here you go~";
			mes "It's your brand";
			mes "new Wedding Ring!";
		}
		close;
	case 2:
		mes "[Wedding Again]";
		mes "Alright. If you ever";
		mes "lose your Wedding Ring,";
		mes "come to me if you happen";
		mes "to need a new one, okay?";
		close;
	}
}