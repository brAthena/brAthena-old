//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: izlude.txt                             |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.2/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Izlude.                  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução PT-BR [Protimus, lututuiBR & Holy]        |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

-	script	::GuiaIzlude	-1,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a ^8B4513Izlude^000000, a cidade-satélite de Prontera.";
	mes "Você precisa de ajuda para navegar pela cidade?";
	F_Navi("[Guia de Izlude]");
	next;
	set .@str$, "Gostaria de verificar quaisquer outros locais?";
	set .@map$, strnpcinfo(4);
	while (1) {
		switch(select("[ Instalações Principais ]:[ Mercadores & Ajudantes ]:Remover indicações do Mini-Mapa:Cancelar")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Academia Cryptura ]:[ Marina ]:[ Aeroplano Internacional ]:[ Arena ]:[ Loja de Equipamentos ]:[ Guilda dos Espadachins ]:[ Associação dos Mercenários ]:[ Forja ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "Aprendizes devem ir na "+F_Navi("Academia Cryptura","iz_ac01,100,39")+" para receber mais informações e treinamento.";
					mes .@str$;
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "Você pode usar a "+F_Navi("Marina",.@map$+",197,205","^B9062F")+" a nordeste para ir para ^8B4513Alberta^000000 ou para a ^8B4513Ilha Byalan^000000.";
					mes "Tenha cuidado com os ^8B4513Calabouços de Izlude^000000 na Ilha Byalan desde que ela é cheia de monstros terríveis.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,197,205,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Aeroplano Internacional",.@map$+",202,75","^0000FF")+" ao sudeste vai para ^8B4513Juno^000000 e ^8B4513Rachel^000000.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,210,73,2,0x0A82FF;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "A "+F_Navi("Arena",.@map$+",213,161","^FF5400")+" no norte é um local perfeito para tesar suas habilidades de combate.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,213,161,3,0xFF5400;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Loja de Equipamentos",.@map$+",110,182","^006400");
					viewpoint 1,108,179,4,0xAAFF00;
					next;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A "+F_Navi("Guilda dos Espadachins",.@map$+",51,172")+" é onde você pode ^800080mudar de classe para Espadachim.^000000";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Assossiação dos Mercenários",.@map$+",97,125","^B9062F"); 
					viewpoint 1,97,125,6,0xFF0000;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("Forja",.@map$+",153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Oficial de Teleporte do Éden ]:[ Funcionária Kafra ]:[ Hipnotizadora ]:[ Ajudante de Recém-Casados ]:[ Guilda dos Assistentes ]:[ Loja de Bichinhos ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "O "+F_Navi("Oficial de Teleporte do Éden",.@map$+",131,148","^0000FF")+" está atualmente dentro da Guilda dos Espadachins.";
					mes "Deixe-me marcar sua localização no seu mini-mapa.";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,131,148,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Funcionária Kafra",.@map$+",128,148","^FF5400"),1;
					viewpoint 1,128,148,9,0xFF5400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("Hipnotizadora",.@map$+",127,175"),1;
					viewpoint 1,127,175,10,0x006400;
					next;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Se você gostaria de estar em Lua de Mel em ^8B4513Jawaii^000000, o paraíso dos recém-casados, por favor, fale com a "+F_Navi("Auxiliar Lua de Mel",.@map$+",180,224","^B9062F")+".";
					mes "Gostaria de verificar quaisquer outros locais?";
					viewpoint 1,180,224,11,0xFF0000;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Guilda dos Assistentes",.@map$+",47,170","^0000FF");
					viewpoint 1,47,170,12,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Loja de Bixinhos","izlude_in,72,98"),1;
					viewpoint 1,108,179,13,0x006400;
					next;
					break; 
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Certo, removerei todas as indicações do seu mini-mapa.";
			mes "Há mais alguma coisa que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Aproveite sua estadia Adeus~!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;

L_Mark:
	mes "[Izlude Guide]";
	mes "Deixe-me marcar a localização de "+getarg(0)+" no seu mini-mapa.";
	mes "Gostaria de checar quaisquer outras localizações?";
	return; 
}

izlude,129,175,4	duplicatr(GuiaIzlude)	Guia#01izlude	8W_SOLDIER
izlude_a,129,175,4	duplicatr(GuiaIzlude)	Guia#01izlude_a	8W_SOLDIER
izlude_b,129,175,4	duplicatr(GuiaIzlude)	Guia#01izlude_b	8W_SOLDIER
izlude_c,129,175,4	duplicatr(GuiaIzlude)	Guia#01izlude_c	8W_SOLDIER
izlude_d,129,175,4	duplicatr(GuiaIzlude)	Guia#01izlude_d	8W_SOLDIER,{}

izlude,121,87,6	script#vpo	Guia#iz	8W_SOLDIER,{
	cutin "prt_soldier",2;
	mes "[Guia de Izlude]";
	mes "Bem-vindo a Izlude, a cidade-satélite de Prontera.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade.";
	mes "Se você tem alguma dúvida, consulte-nos.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Indicações.:Limpar indicações do minimapa.:Tutorial.:Cancelar")) {
		case 1:
			mes "[Guia de Izlude]";
			mes "Por favor, selecione uma das opções.";
			if (.@compass_check == 0) {
				mes "Você gostaria que as indicações permaneçam no mini-mapa?";
				next;
				if (select("Sim.:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000Associação dos Espadachins^000000.:Sede dos Espadachins.:Arena.:Marina de Izlude.:Loja de Armas.:Loja de Ferramentas.:Cancelar.")) {
				case 1:
					mes "[Guia de Izlude]";
					mes "A Associação se encontra na ilha do extremo oeste.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Guia de Izlude]";
					mes "A Sede se encontra na ilha do extremo leste.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Guia de Izlude]";
					mes "A Arena se encontra no extremo norte de Izlude.";
					mes "Porém ela ainda não está funcionando.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Guia de Izlude]";
					mes "Da "+F_Navi("Marina","izlude_a,200,180")+" é possível ir até Alberta ou a ilha de Byalan, nosso calabouço.";
					mes "Ela se localiza ao nordeste.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Guia de Izlude]";
					mes "Você pode facilmente encontrar a "+F_Navi("Loja de Armas","izlude,111,149")+" a noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Guia de Izlude]";
					mes "A "+F_Navi("Loja de Utilidades","izlude,148,148")+" não deve ser difícil de encontrar na parte nordeste da cidade.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Guia de Izlude]";
					mes "Se você deseja apagar as indicações do seu mini-mapa, por favor selecione 'Limpar indicações do minimapa'.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Guia de Izlude]";
			mes "Nós fomos exaustivamente treinados para serví-lo da melhor maneira possível e sem falhas.";
			mes "Assim você tem a garantia das melhores indicações possíveis.";
			next;
			mes "[Guia de Izlude]";
			mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa.";
			mes "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no botão 'mapa' no menu principal.";
			mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			mes "[Guia de Izlude]";
			mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

izlude,133,113,4	duplicatr(GuiaIzlude)	Guia#02izlude	8W_SOLDIER
izlude_a,133,113,4	duplicatr(GuiaIzlude)	Guia#02izlude_a	8W_SOLDIER
izlude_b,133,113,4	duplicatr(GuiaIzlude)	Guia#02izlude_b	8W_SOLDIER
izlude_c,133,113,4	duplicatr(GuiaIzlude)	Guia#02izlude_c	8W_SOLDIER
izlude_d,133,113,4	duplicatr(GuiaIzlude)	Guia#02izlude_d	8W_SOLDIER,{}
//izlude,120,206,4	duplicatr(GuiaIzlude)	Guia#03izlude	8W_SOLDIER,{}