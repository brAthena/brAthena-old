//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: aldebaran.txt                          |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.0/1.2/1.4                         |
//   |--------------------------------------------------------|
//   | Descrição: Guias da Cidade de Aldebaran.               |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   | 1.1 Otimização [Holy & Hold]                           |
//   | 1.2 Tradução (PT-BR) [Hold]                            |
//   | 1.3 Formatação nos Diálogos [lututuiBR & Holy]         |
//   | 1.4 Atualização no sistema de navegação [lututuiBR]    |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

aldebaran,139,63,4	script	Guia#01aldebaran	105,{
	cutin "prt_soldier",2;
	if (checkversion(0)) { // Renewal
		mes "[Guia de Al De Baran]";
		mes "Bem-vindo à ^8B4513Al De Baran^000000, a cidade fronteira.";
		mes "Precisa de alguma ajuda para se localizar?";
		F_Navi("[Al De Baran Guide]");
		next;
		set .@str$,"Gostaria de verificar outras localizações?";
		while (1) {
			switch(select("[ Principais Lugares ]:[ Lojas & Ajudantes ]:Remover marcadores do mini-mapa:Cancelar")) {
			case 1:
				set .@loop,1;
				while (.@loop) {
					switch(select("[ Central da Kafra Corp. ]:[ Guilda dos Alquimistas ]:[ Torre do Relógio ]:[ Hospedaria & Loja de Utilidades ]:[ Reparador & Venda de Equipamentos ]:[ Taverna ]:Menu anterior")) {
					case 1:
						callsub L_Mark, F_Navi("Central da Kafra Corp.","aldebaran,61,229","^B9062F"); 
						mes .@str$;
						viewpoint 1,61,229,0,0xFF0000;
						next;
						break;
					case 2:
						callsub L_Mark, F_Navi("Guilda dos Alquimistas","aldebaran,60,60","^0000FF");
						mes .@str$;
						viewpoint 1,60,60,1,0x0A82FF;
						next;
						break;
					case 3:
						callsub L_Mark, F_Navi("Torre do Relógio","aldebaran,139,133","^FF5400");
						mes .@str$;
						viewpoint 1,139,133,2,0xFFB400;
						next;
						break;
					case 4:
						mes F_Navi("Hospedaria & Loja de Utilidades","aldebaran,197,70","^006400")+" no seu mini-mapa";
						mes .@str$;
						viewpoint 1,197,70,3,0xAAFF00;
						next;
						break;
					case 5:
						mes "[Guia de Al De Baran]";
						mes "Se você gostaria de "+F_Navi("Comprar ou reparar um equipamento","aldebaran,197,70","^7F3300")+" então esse é um lugar que você vai querer visitar.";
						mes .@str$;
						viewpoint 1,72,197,4,0xD2691E;
						next;
						break;
					case 6:
						callsub L_Mark, F_Navi("Taverna","aldebaran,231,106","^800080"); 
						viewpoint 1,231,106,5,0xDA70D6;
						next;
						break;
					case 7:
						set .@loop,0;
						break;
					}
				}
				break;
			case 2:
				set .@loop,1;
				while (.@loop) {
					switch(select("[ Oficial de Teleporte do Éden ]:[ Funcionária Kafra ]:[ Sr. Claus ]:[ Chef Assistente ]:Menu Anterior")) {
					case 1:
						callsub L_Mark, F_Navi("Oficial de Teleporte do Éden","aldebaran,133,119","^B9062F");
						mes .@str$;
						viewpoint 1,133,119,6,0xFF0000;
						next;
						break;
					case 2:
						callsub L_Mark, "^0000FFFuncionária Kafra^000000"; 
						viewpoint 1,143,119,7,0x0A82FF;
						next;
						break;
					case 3:
						mes "Sr. Noel pode teleportar você para "+F_Navi("Lutie","aldebaran,168,168","^006400")+". Vou marcar a localização dele em seu mini-mapa.";
						mes .@str$;
						viewpoint 1,168,168,8,0xAAFF00;
						next;
						break;
					case 4:
						callsub L_Mark, F_Navi("Chef Assistant","aldebaran,165,107","^7F3300");
						mes .@str$;
						viewpoint 1,165,107,9,0xD2691E;
						next;
						break;
					case 5:
						set .@loop,0;
						break;
					}
				}
				break;
			case 3:
				mes "[Guia de Al De Baran]";
				mes "Claro, Vou remover todas as indicações do seu mini-mapa. Mais alguma coisa que eu possa fazer por você?";
				viewpoint 2,1,1,0,0xFFFF00;
				viewpoint 2,1,1,1,0x000000;
				viewpoint 2,1,1,2,0xFF0000;
				viewpoint 2,1,1,3,0xFFFF00;
				viewpoint 2,1,1,4,0xFFFF00;
				viewpoint 2,1,1,5,0xFFFF00;
				viewpoint 2,1,1,6,0xFFFF00;
				viewpoint 2,1,1,7,0xFFFF00;
				viewpoint 2,1,1,8,0xFFFF00;
				viewpoint 2,1,1,9,0xFFFF00;
				next;
				break;
			case 4:
				mes "[Guia de Al De Baran]";
				mes "Tudo bem, aproveite sua estadia!";
				close2;
				cutin "prt_soldier",255;
				end;
			}
		}
	} else { // Pré-Renewal
		mes "[Guarda de Al De Baran]";
		mes "Eu só um simples guarda que você pode encontrar em qualquer outra cidade.";
		mes "Acho que nem tenho um nome...";
		next;
		mes "[Guarda de Al De Baran]";
		mes "Eu sou um dos encarregados do Serviço de Guia de Al de Baran.";
		mes "Deixe-me guiá-lo por nossa cidade!";
		next;
		if (select("Ver guia de locais.:Terminar conversa.") == 1) {
			viewpoint 1,61,229,0,0xFF6633;
			viewpoint 1,72,197,1,0x0000FF;
			viewpoint 1,223,222,2,0x00FFFF;
			viewpoint 1,233,105,3,0x515151;
			viewpoint 1,197,70,4,0x3355FF;
			viewpoint 1,60,60,5,0xFF5555;
			mes "^FF6633+^000000 -> Escritório Central das Kafras";
			mes "^0000FF+^000000 -> Loja de Armas";
			mes "^00FFFF+^000000 -> Guilda dos Feiticeiros (Fechada)";
			mes "^515151+^000000 -> Bar";
			mes "^3355FF+^000000 -> Loja de Items";
			mes "^FF5555+^000000 -> Guilda dos Alquimistas";
			close2;
			cutin "prt_soldier",255;
			end;
		}
		mes "[Guarda de Al De Baran]";
		mes "Nós juramos proteger Al De Baran!";
		mes "As forças do mal sempre serão esmagas pelo poderoso punho da justiça!";
		close2;
		cutin "prt_soldier",255;
		end;
	}
	end;
L_Mark:
	mes "[Al De Baran Guide]";
	mes "Deixe-me marcar a localização de "+getarg(0)+" no seu mini-mapa.";
	return; 
}

aldebaran,243,143,2	duplicatr(Guia#01aldebaran)	Guia#02aldebaran	105
aldebaran,135,243,4	duplicatr(Guia#01aldebaran)	Guia#03aldebaran	105
aldebaran,36,135,6	duplicatr(Guia#01aldebaran)	Guia#04aldebaran	105,{}

aldebaran,133,108,4	script#vpo	Soldado#alde	105,{
	cutin "prt_soldier",2;
	mes "[Guarda de Al De Baran]";
	mes "Eu sou só um simples guarda, que você pode até encontrar em qualquer cidade.";
	next;
	mes "[Guarda de Al De Baran]";
	mes "Quando não estou ocupado protegendo a população de Al de Baran, eu ajudo a guiar aventureiros assim como você.";
	next;
	switch(select("Escritório Principal das Kafras:Loja de Armas:Guilda dos Feiticeiros:Bar:Loja de Items:Guilda dos Alquimistas:Terminar conversa")) {
	case 1:
		viewpoint 1,61,229,0,0xFF6633;
		mes "^FF6633+^000000 -> Escritório Principal das Kafras";
		break;
	case 2:
		viewpoint 1,72,197,1,0x0000FF;
		mes "^0000FF+^000000 -> Loja de Armas";
		break;
	case 3:
		viewpoint 1,223,222,2,0x00FFFF;
		mes "^00FFFF+^000000 -> Guilda dos Feiticeiros (Fechada)";
		break;
	case 4:
		viewpoint 1,233,105,3,0x515151;
		mes "^515151+^000000 -> Bar";
		break;
	case 5:
		viewpoint 1,197,70,4,0x3355FF;
		mes "^3355FF+^000000 -> Loja de Itens";
		break;
	case 6:
		viewpoint 1,60,60,5,0xFF5555;
		mes "^FF5555+^000000 -> Guilda dos Alquimistas";
		break;
	case 7:
		mes "[Guarda de Al De Baran]";
		mes "Estamos ocupados protegendo Al de Baran!";
		mes "As forças do bem sempre prevalecerão sobre o mal~";
		break;
	}
	close2;
	cutin "prt_soldier",255;
	end;
}