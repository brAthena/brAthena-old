//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: 11-25.txt                              |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: ??/1.2/1.0                          |
//   |--------------------------------------------------------|
//   | Descrição: Quests repetíveis para jogadores entre      |
//   | BaseLevel 11-25.                                       |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Megasantos]                      |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

moc_para01,36,38,3	script	Missão [11 - 25]#Tuto	4_BOARD3,{
	if (countitem(6219) < 1) {
		mes "-Você precisa ter um-";
		mes "-^4d4dff'Emblema do Grupo Éden'^000000-";
		mes "-Para receber Missões-";
		close;
	}
	for(set .@i,11124; .@i<11134; set .@i,.@i+1)
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for(set .@i,11114; .@i<11123; set .@i,.@i+1) {
		set .@j, checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes "-Você deve retirar sua-";
			mes "-recompensa antes de começar-";
			mes "-uma nova missão.-";
			mes "";
			mes "-Gerente : Spike-";
			close;
		}
		else if (.@j >= 0) {
			mes "-Você só poderá solicitar-";
			mes "-uma missão de cada vez-";
			mes "";
			mes "-Gerente : Spike-";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes "-Não há tarefas-";
		mes "-abertas para você agora.-";
		close;
	}
	if (BaseLevel > 25) {
		mes "-Essas são Missões-";
		mes "-fáceis demais para o seu nível.-";
		mes "-Procure missões mais-";
		mes "-apropriadas para você-";
		close;
	}
	mes "-Boletim de missão-";
	mes "-para iniciantes.-";
	mes "";
	mes "-Gerente : Spike-";
	next;
	switch(select("Caçar Zangão: Caçar Condor: Coletar Pernas de Gafanhoto: Caçar Rabo de Verme: Caçar Esporos: Controle de Pragas: Caçar Muka: Caçar Familiar: Colecionar Plumas: Coletar Esporos Venenosos: Cancelar")) {
		case 1: callsub L_Quest,11114,"Caçar Zangão"," - Cace 10 ^4d4dff'Zangões'^000000 - "," - noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Caçar Condor"," - Cace 20 ^4d4dff'Condores'^000000 - "," - sul de Morroc. - ";
		case 3: callsub L_Quest,11116,"Coletar Pernas de Gafanhoto"," - Cace 10 ^4d4dff'Rockers'^000000 - "," - sudoeste de Prontera e - "," - traga 10 ^4d4dffPernas de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Caçar Rabo de Verme"," - Cace 20 ^4d4dff'Rabo de Verme'^000000 - "," - sudoeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Caçar Esporos"," - Cace 30 ^4d4dff'Esporos'^000000 - "," - ao redor de Payon. - ";
		case 6: callsub L_Quest,11119,"Controle de Pragas"," - Cace 20 ^4d4dffOvos de Besouro Ladrão^000000 - "," - nos Esgotos de Prontera - "," - e traga 10 ^4d4dffCrisálidas^000000. - ";
		case 7: callsub L_Quest,11120,"Caçar Muka"," - Cace 20 ^4d4dff'Muka'^000000 - "," - sul de Prontera. - ";
		case 8: callsub L_Quest,11121,"Caçar Familiar"," - Cace 20 ^4d4dff'Familiares'^000000 - "," - nos Esgotos - "," - oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Colecionar Plumas"," - Cace 30 ^4d4dff'Peco Pecos'^000000 - "," - sul de Prontera. - ";
		case 10: callsub L_Quest,11123,"Coletar Esporos Venenosos"," - Cace 30 ^4d4dff'Esporo Venenoso'^000000 - "," - dentro da Torre de Geffen e - "," - traga 5 ^4d4dffEsporos Venenosos^000000. - ";
		case 11: mes " - Parando de ler o quadro... - "; close;
	}
	end;
L_Quest:
	if (checkquest(getarg(0),PLAYTIME) == 1) {
		mes "-Missão ^4d4dffCaçar Zangão^000000-";
		mes "-Você já terminou a missão Hoje.-";
		mes "";
		mes "- Gerente : Spike-";
		close;
	}
	mes " - Missão '"+getarg(1)+"' - ";
	for(set .@i,2; .@i<getargcount(); set .@i,.@i+1)
	mes getarg(.@i);
	mes " ";
	mes " - Gerente : Spike - ";
	next;
	if (select("Vou fazer esta missão.:Olhar outras missões.") == 2) {
		mes " - Vou procurar por outras missões. -";
		close;
	}
	for(set .@i,2; .@i<getargcount(); set .@i,.@i+1)
		mes getarg(.@i);
	if (getarg(0) == 11123 && checkquest(11123,PLAYTIME) == 2)
		erasequest 11133;	// Caso especial da "Coleção de Esporos Venenosos"
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for(set .@i,11114; .@i<11124; set .@i,.@i+1) {
		set .@j, checkquest(.@i,HUNTING);
		if (.@j == 2) switch(.@i) {
			case 11114: callsub L_Quest,.@i,"Caçar Zangão",900;
			case 11115: callsub L_Quest,.@i,"Caçar Condor",1800;
			case 11116: callsub L_Quest,.@i,"Coletar Pernas de Gafanhoto",1400,940,10;
			case 11117: callsub L_Quest,.@i,"Caçar Rabo de Verme",2600;
			case 11118: callsub L_Quest,.@i,"Caçar Esporos",3900;
			case 11119: callsub L_Quest,.@i,"Controle de Pragas",2900,915,10;
			case 11120: callsub L_Quest,.@i,"Caçar Muka",3200;
			case 11121: callsub L_Quest,.@i,"Caçar Familiar",3200;
			case 11122: callsub L_Quest,.@i,"Colecionar Plumas",5400;
			case 11123: callsub L_Quest,.@i,"Coletar Esporos Venenosos",6000,7033,5;
		}
		else if (.@j >= 0) {
			mes "[Spike]";
			mes "Você não terminou a sua Missão ainda.";
			mes "Mantenha o bom trabalho. Você está quase terminando.";
			close;
		}
	}
	mes "[Spike]";
	mes "Você está procurando um emprego?";
	mes "Nós sempre temos muito trabalho a fazer.";
	next;
	mes "[Spike]";
	mes "Por que você não olhar em volta para achar algumas tarefas interessantes?";
	close;
L_Quest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		if (getarg(3,0))
			if (countitem(getarg(3)) < getarg(4)) {
				mes "[Spike]";
				mes "Você não terminou sua missão, mas";
				mes "mantenha o bom trabalho. Você está quase pronto.";
				close;
			}
		mes "[Spike]";
		mes "Hmm ... deixe-me ver ~";
		mes "Você completou a Missão 'Caça Ao Zangão'.";
		next;
		mes "[Spike]";
		mes "Bom Trabalho!!!";
		mes "Nosso cliente está tão feliz com seu trabalho. Ele vai apreciar o que você tem feito.";
		mes "Por favor, ajude-nos outra vez. Obrigado!";
		if (getarg(3,0)) delitem getarg(3),getarg(4);
		erasequest getarg(0);
		setquest getarg(0)+10;
		getexp getarg(2),0;
		close;
	}
}