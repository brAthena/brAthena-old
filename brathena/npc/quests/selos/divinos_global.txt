//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Variáveis globais dos Itens Divinos    |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.1/1.2/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: NPC armazenador das variáveis globais.      |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho Criado [Holy]                            |
//   | 1.1 Tradução PT-BR [CaroLPunk]                         |
//   | 1.2 Otimização [lututuiBR]                             |
//   | 1.3 Adicionado NPC de Administração [lututuiBR]        |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

sec_in02,15,170,0	script	Variável Global	4_F_CHNDRESS3,{
	callfunc "F_GM_NPC";
	mes "[Verificador]";
	mes "Digite a senha.";
	next;
	mes "[Verificador]";
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "Escolha um menu.";
		next;
		switch(select("Agora.:No.1.:No.2.:No.3.:No.4.:Resetar:god_sl_1:god_eremes:god_brising:god_mjo_0:god_mjo_1:god_mjo_2:god_mjo_3:god_mjo_4")) {
		case 1:
			mes "^0000FF$God1^000000 = ^FF0000" + $God1 + "^000000.";
			mes "^0000FF$God2^000000 = ^FF0000" + $God2 + "^000000.";
			mes "^0000FF$God3^000000 = ^FF0000" + $God3 + "^000000.";
			mes "^0000FF$God4^000000 = ^FF0000" + $God4 + "^000000.";
			close;
		case 2: callsub L_Number,"$God1"; break;
		case 3: callsub L_Number,"$God2"; break;
		case 4: callsub L_Number,"$God3"; break;
		case 5: callsub L_Number,"$God4"; break;
		case 6:
			mes "[Verificador]";
			mes "Você tem certeza de que deseja resetar a lista de Deuses?";
			mes "Por favor digite a senha.";
			next;
			mes "[Verificador]";
			if (callfunc("F_GM_NPC",68392411,0) == 0) {
				mes "O comando foi cancelado.";
				close;
			}
			mes "Agora, a lista de variáveis de Deuses está sendo resetada.";
			next;
			set $God1,0;
			set $God2,0;
			set $God3,0;
			set $God4,0;
			mes "^0000FF$God1^000000 = ^FF0000" + $God1 + "^000000.";
			mes "^0000FF$God2^000000 = ^FF0000" + $God2 + "^000000.";
			mes "^0000FF$God3^000000 = ^FF0000" + $God3 + "^000000.";
			mes "^0000FF$God4^000000 = ^FF0000" + $God4 + "^000000.";
			close;
		case 7: callsub L_Var,"god_sl_1"; break;
		case 8: callsub L_Var,"god_eremes"; break;
		case 9: callsub L_Var,"god_brising"; break;
		case 10: callsub L_Var,"god_mjo_0"; break;
		case 11: callsub L_Var,"god_mjo_1"; break;
		case 12: callsub L_Var,"god_mjo_2"; break;
		case 13: callsub L_Var,"god_mjo_3"; break;
		case 14: callsub L_Var,"god_mjo_4"; break;
		}
	} else {
		mes "[Verificador]";
		mes "Senha incorreta.";
	}

L_Number:
	input .@input,0,$@god_check2;
	mes "[Verificador]";
	mes "Você gostaria de alterar para "+.@input+"?";
	mes "Digite a senha.";
	next;
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "[Verificador]";
		mes "O commando "+.@input+" foi confirmado.";
		next;
		setd getarg(0),.@input;
		mes getarg(0)+" "+.@input;
		close;
	} else {
		mes "[Verificador]";
		mes "O comando foi cancelado.";
		close;
	}

L_Var:
	input .@input;
	setd getarg(0),.@input;
	next;
	mes "[Verificador]";
	mes "Feito.";
	close;

OnInit:
	// Seals roll at 25/50 in Renewal and 50/100 in Pre-Renewal.
	if (checkversion(0)) {
		set $@god_check1,25;
		set $@god_check2,50;
	} else {
		set $@god_check1,50;
		set $@god_check2,100;
	}
	end;
}