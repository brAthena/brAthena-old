//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: bg_sistema.txt                         |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.3/1.4/1.3                         |
//   |--------------------------------------------------------|
//   | Descrição: Sistema das Batalhas Campais                |
//   |   NPC's da Batalha Campal                              |
//   | - Gerais e Acessores                                   |
//   | - Teleportador da Batalha Campal                       |
//   | - Recrutadora da Batalha Campal                        |
//   | - Kafra e Reparador de itens                           |
//   | - Negociador de Medalhas (Tierra e Flavius)            |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Criado Cabeçalho [Shiraz]                          |
//   | 1.1 Tradução PT-BR [Tyler]                             |
//   | 1.2 Script otimizado [Hold & Holy]                     |
//   | 1.3 Ativado NPC de Administração [lututuiBR]           |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Generais
//============================================================
bat_room,161,158,3	script	Assistente do General#01	4_M_KY_HEAD,{ end; }
bat_room,161,160,3	script	Assistente do General#03	4_M_KY_HEAD,{ end; }

bat_room,160,141,3	script	Príncipe Croix	4_M_CRU_CRUA,{
	cutin "bat_crua1",2;
	mes "[Príncipe Croix]";
	mes "Sábio aventureiro, por que você não nos empresta seu poder e juntos vamos rumo à vitória?";
	next;
	cutin "bat_crua2",2;
	if (select("Qual o motivo desta batalha?:Me conte mais sobre o General Guillaume.") == 1) {
		mes "[Príncipe Croix]";
		mes "O grande Rei de Maroll, Marcel Marollo VII, está muito doente ultimamente.";
		mes "A grande Majestade então declarou que deixará o futuro trono de Maroll para mim ou para o 3º príncipe, o General Guillaume.";
		next;
		mes "[Príncipe Croix]";
		mes "O General Guillaume pode ter uma pequena vantagem nesta batalha, já que ele é o grande General de Maroll mas, isso não garante sua vitória instantânea nesta guerra.";
		mes "Eu quero ganhar essa batalha e assim trazer prosperidade ao povo de Maroll. Eles já sofreram o bastante com tudo isso...";
	} else {
		mes "[Príncipe Croix]";
		mes "O 3º Príncipe Guillaume é o grande General de Maroll.";
		mes "É perda de tempo eu tentar lhe explicar o quão grande líder ou chefe militar ele é, já que é ele quem comanda o grande poder militar de Maroll.";
		next;
		mes "[Príncipe Croix]";
		mes "Infelizmente, há uma coisa que ele e seus seguidores desconhecem:";
		mes "Será que as pessoas de Maroll realmente querem que ele gaste tantos investimentos com o poder militar?";
		mes "Nós já sofremos o bastante com as guerras.";
		mes "Eu acredito que as armas não são o melhor caminho para trazer prosperidade à uma nação.";
		next;
		mes "[Príncipe Croix]";
		mes "Eu não quero derramar mais sangue mas, eu não tenho escolha e tenho que lutar pela possibilidade de paz e pelo bem do meu povo.";
	}
	next;
	if (select("Sim, eu quero unir-me às suas forças.:Finalizar a conversa.") == 1) {
		cutin "bat_crua1",2;
		mes "[Príncipe Croix]";
		mes "Muito obrigado. Eu sinto que a vitória se aproxima com aventureiros como você.";
		mes "Agora por favor, desça as escadas e junte-se aos seus camaradas. Aprimorem suas habilidades e se preparem para a batalha!";
	} else {
		mes "[Príncipe Croix]";
		mes "Por Maroll!";
	}
	close2;
	cutin "",255;
	end;
}

bat_room,161,140,3	script	Assistente do Principe#01	4_M_CRU_HEAD,{ end; }
bat_room,161,142,3	script	Assistente do Principe#02	4_M_CRU_HEAD,{ end; }

bat_room,160,159,3	script	General Guillaume	4_M_KY_KIYOM,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Audacioso aventureiro, nós precisamos de suas habilidades para vencer esta batalha.";
	next;
	cutin "bat_kiyom1",2;
	if (select("Qual o motivo desta batalha?:Me conte mais sobre o Príncipe Croix.") == 1) {
		mes "[General Guillaume]";
		mes "Nosso grande Rei, Marcel Marollo VII, tem andando muito doente.";
		mes "Sua Majestade declarou que ele escolherá entre eu ou o Príncipe Croix para ser o próximo rei dentre todos os seus 9 filhos.";
		next;
		mes "[General Guillaume]";
		mes "Dois reis não podem dividir uma nação! Somente um sairá vitorioso da batalha, será apontado por nossa Majestade e então terá direito ao trono.";
		next;
		mes "[General Guillaume]";
		mes "Entretanto, e de qualquer maneira, isto não é somente uma batalha entre nós. Esta batalha irá determinar o futuro desta nação.";
		mes "Eu juro por minha honra e vou provar que sou aquele que pode proteger Maroll das ameaças externas.";
	} else {
		mes "[General Guillaume]";
		mes "O 5º Príncipe Croix é atualmente entitulado como o Primeiro Ministro de Maroll.";
		mes "Ele acha que todas as questões nacionais de um país podem ser discutidas e determinadas em uma mesa";
		mes "e, acredita em uma coexistência pacífica com os outros países.";
		next;
		mes "[General Guillaume]";
		mes "Ele é muito ignorante para admitir que o que ele chama de paz é construída sobre incontáveis vidas que são sacrificadar na guerra. Enquanto que os cidadãos normais e os da classe alta vivem normalmente, alheios aos horrores que lhes permitiram viver desta maneira.";
		next;
		mes "[General Guillaume]";
		mes "Ele é muito ingênuo para entender a realidade....";
		mes "Eu não posso deixar Maroll nas mãos de alguém que acredita que viva dentro de um sonho!";
		next;
		mes "[General Guillaume]";
		mes "Suas crenças irrealistas vão afogar este país em uma situação de pobreza e tornar as pessoas mais fracas. Se ele se tornar rei, Maroll nunca descansará dos sofrimentos vindos de outros países.";
		mes "Eu quero ensiná-lo o que fazer com que este pequeno país tão poderoso e próspero. É esse o poder militar!";
	}
	next;
	if (select("Sim, eu quero me unir-me às suas forças.:Finalizar a conversa.") == 1) {
		cutin "bat_kiyom2",2;
		mes "[General Guillaume]";
		mes "Seja bem-vindo ao meu exército, camarada.";
		mes "Seus olhos me dizem que você é um soldado no qual eu posso confiar.";
		next;
		mes "[General Guillaume]";
		mes "Agora suba as escadas e começe a pôr em prática suas habilidades com seus novos companheiros.";
		mes "Tenho certeza de que te receberão de braços abertos!";
	} else {
		mes "[General Guillaume]";
		mes "Eu serei o único que irá capturar aquela bandeira!";
	}
	close2;
	cutin "",255;
	end;
}

// Bandeiras
//============================================================
//bat_room,140,160,3	script	Base Guillaume#roomflag1	1_FLAG_LION,{ end; }
//bat_room,167,160,3	script	Base Guillaume#roomflag2	1_FLAG_LION,{ end; }
//bat_room,140,139,3	script	Base Croix#roomflag1	1_FLAG_EAGLE,{ end; }
//bat_room,167,139,3	script	Base Croix#roomflag2	1_FLAG_EAGLE,{ end; }

// Teleportador da Batalha Campal
//============================================================
bat_room,148,150,5	script	Teleportador#Battlefield	4_F_TELEPORTER,{
	mes "[Teleportador]";
	mes "Você deseja sair do campo de batalha? Utilize meus serviçoes para retornar a uma cidade.";
	next;
	if (select("Sair.:Não sair.") == 2) {
		mes "[Teleportador]";
		mes "Eu estarei por aqui sempre que precisar dos meus serviços.";
		close;
	}
	mes "[Teleportador]";
	switch(bat_return) {
	default:
	case 1:
		setarray .@mapname$[0],"Prontera","prontera";
		setarray .@xy[0],116,72;
		break;
	case 2:
		setarray .@mapname$[0],"Morroc","moc_ruins";
		setarray .@xy[0],152,48;
		break;
	case 3:
		setarray .@mapname$[0],"Al De Baran","aldebaran";
		setarray .@xy[0],168,112;
		break;
	case 4:
		setarray .@mapname$[0],"Geffen","geffen";
		setarray .@xy[0],120,39;
		break;
	case 5:
		setarray .@mapname$[0],"Payon","payon";
		setarray .@xy[0],161,58;
		break;
	case 6:
		setarray .@mapname$[0],"Lighthalzen","lighthalzen";
		setarray .@xy[0],159,93;
		break;
	case 7:
		setarray .@mapname$[0],"Rachel","rachel";
		setarray .@xy[0],115,124;
		break;
	}
	mes "Você será enviado para "+.@mapname$[0]+".";
	close2;
	warp .@mapname$[1],.@xy[0],.@xy[1];
	end;
}

prontera,123,83,3	script	Emissária de Maroll::BatRecruit	4_F_JOB_KNIGHT,{
	mes "[Emissária de Maroll]";
	mes "Olá caro aventureiro!";
	mes "Eu sou uma cavaleira vinda de um lugar distante chamado Reino de Maroll.";
	next;
	mes "[Emissária de Maroll]";
	mes "Os dois príncipes do reino estão agora batalhando pelo trono de Maroll e estão precisando de pessoas como você para se juntar á luta.";
	mes "Você gostaria de compartilhar seus poderes com um dos príncipes do Reino de Maroll nesta grande guerra?";
	next;
	if (select("Juntar-se.:Não juntar-se.") == 2) {
		mes "[Emissária de Maroll]";
		mes "Em todo caso eu estarei sempre aqui neste mesmo lugar. Sinta-se livre para voltar quando se sentir interessado.";
		close;
	}
	mes "[Emissária de Maroll]";
	mes "Que o deus da guerra te abençoe e esteja com você.";
	close2;
	set .@mapname$, strnpcinfo(4);
	if (.@mapname$ == "prontera") set bat_return,1;
	else if (.@mapname$ == "moc_ruins") set bat_return,2;
	else if (.@mapname$ == "aldebaran") set bat_return,3;
	else if (.@mapname$ == "geffen") set bat_return,4;
	else if (.@mapname$ == "payon") set bat_return,5;
	else if (.@mapname$ == "lighthalzen") set bat_return,6;
	else if (.@mapname$ == "rachel") set bat_return,7;
	else set bat_return,1;
	warp "bat_room",154,150;
	end;
}

moc_ruins,75,162,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit2	4_F_JOB_KNIGHT
aldebaran,146,109,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit3	4_F_JOB_KNIGHT
geffen,109,66,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit4	4_F_JOB_KNIGHT
payon,189,105,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit5	4_F_JOB_KNIGHT
lighthalzen,153,86,5	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit6	4_F_JOB_KNIGHT
rachel,149,138,3	duplicate(BatRecruit)	Emissária de Maroll::BatRecruit7	4_F_JOB_KNIGHT

// Pontos de teleporte adicionais
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Vazio
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Livre
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Livre
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Livre
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Livre
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Livre
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Vazio
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Vazio
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	4_F_KAFRA9,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Reparador
//============================================================
bat_room,138,144,4	script	Reparador#bg	4_M_04,{
	callfunc "repairmain","Reparador";
	end;
}

// NPC de Administração
//============================================================
bat_room,1,151,3	script	Dispositivo#batgnd	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "O comando foi cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Eu posso te ajudar?";
		next;
		switch(select("Fechar o Campo de Batalha.:Abrir o Campo de Batalha.:Resetar a01.:Resetar b01.:Resetar a02.:Resetar b02.")) {
		case 1:
			setarray .@bg1$[0], "Diretor Oficial de Tierra#01a", "Diretor Oficial de Tierra#02a", "Diretor Oficial de Tierra#01b", "Diretor Oficial de Tierra#02b", "Oficial de Flavius#01a", "Oficial de Flavius#01b", "Oficial de Flavius#02a", "Oficial de Flavius#02b";
			for(set .@k,0; .@k < getarraysize(.@bg1$[0]); set .@k, .@k+1)
				disablenpc .@bg1$[.@k];
			break;
		case 2:
			setarray .@bg2$[0],"Diretor Oficial de Tierra#01a","Diretor Oficial de Tierra#02a","Diretor Oficial de Tierra#01b","Diretor Oficial de Tierra#02b","Oficial de Flavius#01a","Oficial de Flavius#01b","Oficial de Flavius#02a","Oficial de Flavius#02b";
			for(set .@i, 0; .@i < getarraysize(.@bg2$); set .@i, .@i+1)
				enablenpc .@bg2$[.@i];
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Concluído";
		close;
	}
}

// Troca de Emblemas
//============================================================
bat_room,160,150,3	script	Erundek	4_M_MANAGER,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere aí um minuto! -";
		mes "- Atualmente você está -";
		mes "- carregando itens demais. -";
		mes "- Por favor tente novamente após -";
		mes "- você descarregar um pouco do peso. -";
		close;
	}
	mes "[Erundek]";
	mes "Você possui os Emblemas da Batalha Campal?";
	mes "Eu posso trocar Emblemas de Bravura e Emblemas de Valor por itens como recompensa.";
	next;
	if (select("Trocar os Emblemas.:Checar o catálogo.") == 2) {
		mes "[Erundek]";
		mes "Nós temos muitos itens como recompensa, então por favor dê uma olhada antes e compre com calma.";
		close2;
		readbook 11010,1;
		end;
	}
	mes "[Erundek]";
	mes "Por qual tipo de itens você deseja fazer a troca?";
	mes "Para checar maiores informações sobre os itens de recompensa, por favor use o nosso ^3131FFCatálogo^000000.";
	next;
	switch(select("Armas:Equipamentos:Acessórios:Consumíveis")) {
	case 1:
		mes "[Erundek]";
		mes "Você escolheu a opção ^3131FFArmas^000000.";
		mes "As seguintes armas estão disponíveis para troca por Emblemas da Batalha Campal.";
		mes "Por favor, note que os itens que utilizam ^3131FFEmblemas de Bravura estão indicados como (EB)^000000, e os que utilizam ^3131FFEmblemas de Valor como (EV)^000000.";
		next;
		switch(select("Adagas/Espadas 1M/Espadas 2M/Lanças:Cajados/Maças/Machados 2M/Huuma Shurikens:Arcos/Katares/Instrumentos Musicais/Chicotes:Livros/Soqueiras:Revólveres/Rifles/Metralhadoras/Espingardas/Lança-Granadas")) {
		case 1:
			mes "[Erundek]";
			mes "Estão disponíveis nesta categoria os seguintes itens: ^3131FFAdagas / Espadas de Uma Mão / Espadas de Duas Mãos / Lanças^000000.";
			next;
			setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
			break;
		case 2:
			mes "[Erundek]";
			mes "Estão disponíveis nesta categoria os seguintes itens: ^3131FFCajados / Maças / Machados de Duas Mãos / Huuma Shurikens^000000.";
			next;
			setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
			break;
		case 3:
			mes "[Erundek]";
			mes "Estão disnponíveis nesta categoria os seguintes itens: ^3131FFArcos / Katares / Instrumentos Musicais / Chicotes^000000.";
			next;
			setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
			break;
		case 4:
			mes "[Erundek]";
			mes "Estão disnponíveis nesta categoria os seguintes itens ^3131FFLivros / Soqueiras^000000.";
			next;
			setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
			break;
		case 5:
			mes "[Erundek]";
			mes "Estão disponíveis nesta categoria os seguintes itens: ^3131FFRevólveres / Rifles / Metralhadoras / Espingardas / Lança-Granadas^000000.";
			next;
			setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
			break;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
			set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
		set .@i, (select(.@menu$)-1)*2;
		set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
		mes "[Erundek]";
		mes "Você escolheu o item ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
		mes "Você pode trocar este item por ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000.";
		mes "Você deseja realizar a transação?";
		next;
		switch(select("Não trocar.:Trocar")) {
		case 1:
			break;
		case 2:
			mes "[Erundek]";
			mes "Você gostaria de gastar ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 e receber o item ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
			next;
			mes "[Erundek]";
			mes "Lembre-se, as recompensas da Batalha Campal são ^FF0000VÍNCULADAS à este personagem^000000. Você tem certeza que deseja este item?";
			next;
			switch(select("Sim:Não")) {
			case 1:
				mes "[Erundek]";
				if (countitem(.@Weapons[.@i+1]) >= 100) {
					mes "Obrigado, transação concluída.";
					delitem .@Weapons[.@i+1],100;
					getitem .@Weapons[.@i],1;
				}
				else mes "Eu sinto muito, mas você não possui Emblemas suficientes para realizar a transação.";
				close;
			case 2:
				break;
			}
			break;
		}
		mes "[Erundek]";
		mes "Você precisa de mais tempo para checar melhor os itens?";
		close;
	case 2:
		mes "[Erundek]";
		mes "Você escolheu a opção ^3131FFArmadura^000000.";
		mes "Os seguintes equipamentos estão disponíveis para troca por Emblemas da Batalha Campal.";
		next;
		switch(select("Capuz/Sapato:Armadura")) {
		case 1:
			setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
			break;
		case 2:
			setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
			break;
		}
		break;
	case 3:
		mes "[Erundek]";
		mes "Você escolheu a opção ^3131FFAcessórios^000000.";
		mes "Você pode trocar seus Emblemas por sua Medalha de mérito de acordo com a sua classe, como a seguir:";
		next;
		setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
		set .@menu1$,"Justiceiro:Espadachim/Mestre Taekwon:Gatuno:Noviço:Mago:Arqueiro:Mercador";
		break;
	case 4:
		mes "[Erundek]";
		mes "Você escolheu a opção ^3131FFConsumíveis^000000.";
		mes "Os seguintes consumíveis estão disponíveis para troca por Emblemas da Batalha Campal:";
		next;
		setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
		break;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Você escolheu ^3131FF"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "Este item só pode ser usado pelas classes Espadachim, suas evoluções e Mestre Taekwon."; break;
		case 2721: mes "Este item só pode ser usado pela classe dos Gatunos e suas evoluções."; break;
		case 2722: mes "Este item só pode ser usado pela classe dos Noviços e suas evoluções."; break;
		case 2723: mes "Este item só pode ser usado pela classe dos Magos e suas evoluções."; break;
		case 2724: mes "Este item só pode ser usado pela classe dos Arqueiros e suas evoluções."; break;
		case 2725: mes "Este item só pode ser usado pela classe dos Mercadores e suas evoluções."; break;
		case 2733: mes "Este item só pode ser usado pela classe dos Justiceiros."; break;
		default: break;
	}
	mes "Você pode trocar este item com ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+" ou "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "Deseja realizar a transação?";
	next;
	if (select("Não trocar.:Trocar") == 1) {
		mes "[Erundek]";
		mes "Você precisa de mais tempo para checar melhor os itens?";
		close;
	}
	mes "[Erundek]";
	mes "Por quais emblemas você deseja trocar?";
	mes "Você precisa de ^3131FF"+.@Items[.@i+1]+" Emblemas^000000 para realizar a transação.";
	next;
	if (.@item[0] < 12269 || .@item[0] > 12273 ) {
		mes "[Erundek]";
		mes "Lembre-se, as Recompensas das Batalhas serão ^FF0000VÍNCULADAS à este personagem^000000. Tem certeza que quer mesmo este item?";
		next;
	}
	set .@j, select("Emblemas de Bravura.:Emblemas de Valor.:Cancelar");
	mes "[Erundek]";
	if (.@j == 3) {
		mes "Você cancelou a transação.";
		close;
	}
	set .@cost, ((.@j==1) ? 7828 : 7829);
	if (countitem(.@cost) >= .@Items[.@i+1]) {
		mes "Obrigado, transação concluída.";
		delitem .@cost, .@Items[.@i+1];
		getitem .@Items[.@i],1;
	} else
		mes "Você não possui "+getitemname(.@cost)+"s.";
	close;
}