//  ____________________________________________________________
// /                                                            \
// |       _           _   _   _                                |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _           |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |          |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |          |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|          |
// |                                                            |
// |                    brAthena Script                         |
// |------------------------------------------------------------|
// | Nome do Script: Evento para Resetar as Habilidades 		|
// |------------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                               |
// |------------------------------------------------------------|
// | Versões eA/brA: 1.02/1.1                                   |
// |------------------------------------------------------------|
// | Descrição: Resetar as habilidades por uma taxa de zenys	|
// | Necessário Nv. Mínimo de Base 60, para poder participar.	|
// | A cada Nível de Base precisa de 20.000 Zenys.              |
// |------------------------------------------------------------|
// | Changelog:                                                 |
// | 1.0 Script Traduzido [Heaven]                              |
// | 1.1 Script Revisado [Protimus]                             | 
// \____________________________________________________________/

yuno,138,187,4	script	Hipnotizadora	124,{
        set @npcname$,"[^D5A500Hipnotizadora^000000]";
        mes @npcname$;

        if(MISC_QUEST & 1024){
                mes "Desculpe mas você só pode completar este evento uma vez.";
                close;
        }
        if(BaseLevel < 60) {
                mes "Por favor, retorne quando chegar ao Nv. de Base 60.";
                close;
        }
        if(SkillPoint != 0) {
                mes "Você precisará usar todos os seus pontos de habilidade caso queira que eu continue.";
                close;
        }
        if(Weight || checkfalcon() || checkcart() || checkriding()) {
                mes "Você gostaria de resetar suas habilidades?";
                mes "Desculpe-me, mas...";
                next;
                mes @npcname$;
                if(Weight) {
                        mes "Você não pode resetar suas habilidades enquanto estiver carregando algum item.";
                } else if(checkcart())
                        mes "Por favor, livre-se do seu carrinho para continuarmos.";
                else if(checkfalcon())
                        mes "Por favor, livre-se do seu Falcão para continuarmos.";
                else if(checkriding())
                        mes "Por favor, livre-se do seu Peco Peco para continuarmos.";
                next;
                mes @npcname$;
                mes "Volte em breve!";
                close;
        }
        mes "Olá, aventureiro!";
		mes "Meu dever é ajudar os iniciantes que se arrependem de não ter aprendido suas habilidades do jeito que gostariam.";
		next;
		mes "Seu nome é ^0080FF"+strcharinfo(0)+"^000000, certo?";
        mes "Como posso ajudá-lo?";
        next;
        menu "^009500Redistribuição de Pontos de Habilidade?^000000",L_Info,"^00B6FFRedistribuir os Pontos de Habilidade.^000000",L_Reset,"^000088Não se preocupe.^000000",-;

        mes @npcname$;
        mes "Se você quiser ajuda, sabe onde me encontrar!";
        close;

L_Info: 
        mes @npcname$;
		mes "A Redistribuição de Pontos de Habilidade dá outra chance aos aventureirosde distribuir seus Pontos.";
		mes "Se não estiver satisfeito com suas habilidades atuais, então você pode usufruir meu serviço.";
		next;
        mes "Resetar as habilidades não é gratuito!!";
        mes "O gasto para resetar as habilidades é de^D5A50020000 Zeny x Nível de Base^000000.";
        mes "Pois é ...cada Nível de Base custa 20000 Zenys para resetar suas habilidades.";
        next;
		mes @npcname$;
		mes "Além disso, você só poderá usar meu serviço caso seu level de base seja maior que 60!";
		next;
		mes "Lembre-se que só é possível redistribuir seus pontos de Habilidade se não estiver carregando nenhum item com você.";
		mes "Por favor, use antes o serviço de Armazenagem Kafra para reduzir seu peso.";
        close;
        
L_Reset:
        mes @npcname$;
        mes "Antes de resetar suas habilidades, você deve me dizer seu Nível de Base."; 
        next;
        mes "^D5A500[" + strcharinfo(0) + "]^000000";
        mes "Meu ^AA00AANível de Base é " + BaseLevel + "^000000."; 
        next;
        set @zeny,BaseLevel*20000;
        mes @npcname$;
        mes "O total de zenys necessários para resetar as habilidades é de ^529DFF" + @zeny + "Zenys^000000.";
        next;
        if(Zeny < @zeny) {
                mes @npcname$;
                mes "Parece que você não tem dinheiro suficiente.";
                emotion e_hmm;
                close;
        }
        set Zeny, Zeny-@zeny;
        set MISC_QUEST,MISC_QUEST | 1024;
        resetskill;
        mes @npcname$;
		mes "...Acorde.";
		mes "Viu? Suas habilidades agora estão redistribuídas.";
		mes "Muito obrigada por usufruir do meu serviço, e boa sorte em todas as suas aventuras...";
        emotion e_thx;
        logmes "SKILL RESET EVENT";
        close;
}