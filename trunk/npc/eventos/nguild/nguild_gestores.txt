//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: WoE Managers for N Guild Castles       |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.2/1.0                                |
// |--------------------------------------------------------|
// | Descrição:                                             |
// | Break down of arguments used in the function:          |
// |   arg(0): name of Castle Manager                       |
// |   arg(1): name of guild castle.                        |
// |   arg(2): x1 coordinate for warp to treasure room      |
// |   arg(3): y1 coordinate for warp to treasure room      |
// |   arg(4): guild script suffix for kafra, etc.          |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Tradução Português BR [cleyton]                    |
// | 					   			 						|
// \________________________________________________________/

//==============================================
function	script	F_GldManager	{

	set @GID, GetCastleData(getarg(1),1);
	mes "[ "+getarg(0)+" ]";
	if (@GID == 0){
		mes "Eu estou aguardando o meu mestre.  Bravo aventureiro, siga o seu destino!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "Eu estou aqui para seguir ordens do mestre ^5533FF" + getguildmaster(@GID) + "^000000'! Hey! Você nem faz parte do Clã!";
		mes "Where are the guardians? Destroy these intruders!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "Você não é  ^5533FF" + getguildmaster(@GID) + "^000000! Eu sigo somente as ordens do mestre ^5533FF" + getguildmaster(@GID) + "^000000'";
		return 0;
	}

	mes "Bem vindo mestre ^5533FF" + getguildmaster(@GID) + "^000000 ! Darei assistência no que puder!";
	next;

	menu "Serviço de Kafras / Dismissal",M_Kaf, "Entrar na sala do tesouro",M_Treas, "Cancelar",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (GetCastleData(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "Mestre, você não possui contrato com o escritório central das Kafras.";
			mes "Para contratar uma Kafra, primeiro você precisa possuir a habilidade de Clã ^5533FFContrato com Kafra^000000.";
			return;
		}

		L_Hire:
			mes "Você gostaria de contratar os serviços de uma Kafra? Você vai precisar de ^5533FF10,000 Zeny^000000 para isto... ";
			next;
			menu "Contratar Kafra.",-,"Cancelar",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000){
					mes "Mestre, você não possui verba para contratar uma Kafra. A contratação foi cancelada.";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "Kafra Staff#"+getarg(4);
				SetCastleData getarg(1),9,1;
				mes "Você criou um contrato com o escritório central das Kafras.";
				next;
				cutin "kafra_01",2;
				mes "[ Kafra ]";
				mes "Como você está? Eu estou aqui para providenciar meus serviços úteis! Eu farei o melhor para servi-lo bem.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "Your employment contract lasts ^5533FF1 month^000000.  After this term is over you will have to create a new contract.";
				mes "Acho que os serviços da Kafra irão beneficiar os membros do Clã.";
				return;

			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "Como você quiser mestre, mas sugiro que contrate uma kafra novamente o mais rápido possível!";
				return;

		L_Dismiss:
			mes "Você gostaria de dispensar a Kafra atual?";
			next;
			menu "Dispensar",-,"Cancelar",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[ Kafra ]";
				mes "Have I done anything wrong? If I did, will you please forgive me?";
				next;
				menu "Dispensar",-,"Cancelar",ssM_KafEnd2;

					mes "[ Kafra ]";
					mes "É uma pena que não irei mais servir ao seu Clã....";
					next;
					disablenpc "Kafra Staff#"+getarg(4);
					SetCastleData getarg(1),9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "A kafra foi dispensada.  Mas... Nós precisamos ter uma Kafra assim que possível!";
					return;
				ssM_KafEnd2:
					mes "[ Kafra ]";
					mes "Obrigado mestre! Eu farei o meu melhor! ^^.";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "Mestre, eu acho que é melhor manter a Kafra atual, pois ela está fazendo o seu melhor";
				return;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "Deseja ir para sala do tesouro? Somente você, mestre do clã, pode entrar na sala do tesouro.";
		next;
		menu "Entrar na sala do tesouro.",-,"Cancelar",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "Por favor, me siga pelo caminho da passagem secreta.";
			mes "Você deve puxar a alavanca secreta para poder sair";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "Tesouros são produzidos todos os dias.";
			mes "Você deve recolher eles todos os dias ou eles podem desaparecer.";
			return;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "Como você desejar mestre.";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	Frolo	55,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	Leiber	55,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	Dundar	55,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	Thefton	55,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}
